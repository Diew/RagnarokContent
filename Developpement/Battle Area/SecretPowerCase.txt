// [========================================================]
// [============    Battle Arena - Mistery Case   ==========]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - The Mistery Case System				    ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [                                                        ]
// [========================================================]
// [                                                        ]
// [========================================================]

pvp_n_4-1,50,49,0	script	Mistery Case::alabamc	HIDDEN_WARP_NPC,2,2,{
	if(.spokenCount == 0)
	{
		goto OnGetSuperPower;
	}
	else
	{
		goto OnLandmine;
	}
	close;

OnPCLoadMapEvent:
	if (strcharinfo(3) == $mCMap$)
	{
		//finally, check if event should start
		if (getarraysize($MCMembers) == $maxMCMemberCount && $misterycaseon != 2)
		{
			goto StartMC;
		}
	}
	end;

StartMC:
	pvpon $mCMap$;
	mapannounce $mCMap$, "[Mistery Case] The Mistery Case starts!", bc_red;
	initnpctimer;
	set $misterycaseon, 2;
	set .totalCount, 0;
	goto OnPopMisteryCase;
	end;
	
OnLandmine:
	set .spokenCount, .spokenCount + 1;
	switch(rand(0,5))
	{
		case 0:
			//Stun
			sc_start SC_STUN,10000,0,2500,SCFLAG_NONE;
			break;
		case 1:
			//Stone Curse
			sc_start SC_STONE,10000,0,2500,SCFLAG_NONE;
			break;
		case 2:
			//Freeze
			sc_start SC_FREEZE,10000,0,2500,SCFLAG_NONE;
			break;
		case 3:
			//Masquerade
			sc_start SC__LAZINESS,10000,0,2500,SCFLAG_NONE;
			break;
		case 4:
			//Weakness
			sc_start SC__WEAKNESS,10000,0,2500,SCFLAG_NONE;
			break;
		case 5:
			//Mine
			specialeffect 106;
			charcommand "#nuke " + strcharinfo(0);
			break;
	}
	if(.spokenCount == .maxSpokenCount)
	{
		setnpcdisplay strnpcinfo(3), HIDDEN_WARP_NPC;
		set .mcPopTimer, 1;
	}
	end;
	
OnGetSuperPower:
	set .spokenCount, 1;
	switch(rand(0,5))
	{
		case 0:
			//Baphomet
			disguise 1039;
			sleep 5000;
			unitskilluseid getcharid(3), NPC_SPLASHATTACK, 10; 
			sleep 1000;
			undisguise;
			break;
		case 1:
			//Ifrit
			disguise 1832;
			sleep 5000;
			unitskilluseid getcharid(3), GN_DEMONIC_FIRE, 10; 
			sleep 1000;
			undisguise;
			break;
		case 2:
			//Beelzebub
			disguise 1873;
			sleep 5000;
			unitskilluseid getcharid(3), NPC_HELLJUDGEMENT, 10; 
			sleep 1000;
			undisguise;
			break;
		case 3:
			//Ktullanux
			disguise 1779;
			sleep 5000;
			unitskilluseid getcharid(3), WL_JACKFROST, 10; 
			sleep 1000;
			undisguise;
			break;
		case 4:
			//Valkyrie
			disguise 1751;
			sleep 5000;
			unitskilluseid getcharid(3), NPC_POWERUP, 10; 
			sleep 1000;
			undisguise;
			break;
		case 5:
			//Wounded Morroc
			disguise 1917;
			sleep 5000;
			unitskilluseid getcharid(3), NPC_EARTHQUAKE, 10; 
			sleep 1000;
			undisguise;
			break;
	}
	end;
	
OnPopMisteryCase:
	if($misterycaseon == 2)
	{
		disablenpc strnpcinfo(3);
		
		do
		{		
			set .movex,rand(0,300);
			set .movey,rand(0,300);
			set .map$,strcharinfo(3);
		}
		while(!checkcell(.map$,.movex,.movey,cell_chkpass));
		movenpc strnpcinfo(3),.movex,.movey;				
		enablenpc strnpcinfo(3);
		
		set .spokenCount, 0;
		setnpcdisplay strnpcinfo(3), "CHEST";
		mapannounce $mCMap$, "[Mistery Case] The Mistery Case appeared!", bc_blue;
	}
	end;

OnTimer10000:
	set .totalCount, .totalCount + 1;
	if (.totalCount == $battleDuration)
	{
		callsub OnCheckVictory;
	}

	if(.mcPopTimer > 0)
	{
		set .mcPopTimer, .mcPopTimer + 1;
		if(.mcPopTimer >= .delayBetweenCount)
		{
			goto OnPopMisteryCase;
		}
	}
	
	setnpctimer 0;
	end;

OnCheckVictory:
	//every minute, check for members
	set .@winner, -1;
	set .@highest, 0;
	for(set .@i,0; .@i<getarraysize($MCMembers); set .@i,.@i+1)
	{
		set .@attached, attachrid($MCMembers[.@i]);
		if (getarraysize($MCMembers[.@i]) == 0 || .@attached == 0)
		{
			deletearray $MCMembers[.@i], 1;
		}
		else
		{
			if(MCPOINTS > .@highest)
			{
				set .@highest, MCPOINTS;
				set .@winner, getcharid(3);
			}
		}
	}

	if(.@winner != -1)
	{
		attachrid(.@winner);
		pvpoff $mCMap$;
		announce "[Mistery Case] " + strcharinfo(0) + " just win the Mistery Case!", bc_red|bc_all;
		mapannounce $mCMap$, "[Mistery Case] Congratulations, " + strcharinfo(0) + "!", bc_red;
		mapannounce $mCMap$, "[Mistery Case] you will now be rewarded.", bc_red;
		set Zeny, Zeny + $MCEntryPrice * ($maxMCMemberCount - 1);
		set MCPOINTS, MCPOINTS + .pointPerVictory;
		set Zeny, Zeny + MCPOINTS * 10000;
		//callfunc("AddPoints",getcharid(3),.pointPerVictory);
		set $misterycaseon, 0;
	}
	else
	{
		announce "[Mistery Case] ends without winner...", bc_red|bc_all;
		set $misterycaseon, 0;
	}
	return;
	
OnBoardAsked:
	set .@caller, getcharid(3);
	dispbottom "!!Mistery Case Points Board!!";
	for(set .@i,0; .@i<getarraysize($MCMembers); set .@i,.@i+1)
	{
		set .@attached, attachrid($MCMembers[.@i]);
		if (.@attached == 0)
		{
			deletearray $MCMembers[.@i], 1;
		}
		else
		{
			set .@message$, "	~" + strcharinfo(0) + ": " + MCPOINTS;
			if(attachrid(.@caller))
				dispbottom .@message$;
		}
	}
	end;
	
OnPCLoginEvent:
	set MCPOINTS, 0;
	end;

OnPCDieEvent:
	if (strcharinfo(3) == $mCMap$)
	{
		addtimer 5000, strnpcinfo(3) + "::OnHealAfterDeath";
		
		attachrid(killerrid);
		
		set MCPOINTS, MCPOINTS + .pointPerKill;
		dispbottom "You've earn " + .pointPerKill + " points. Your current balance is " + MCPOINTS;
	}
	end;

OnHealAfterDeath:
	dispbottom "[Valkyrie] You received the War Goddess benediction!";
	percentheal 100, 100;
	if(.buffs == 1)
	{
		specialeffect2 EF_INCAGILITY; sc_start SC_INC_AGI,240000,10;
		specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	}
	end;

OnInit:
	set $mCMap$, "pvp_n_4-1";
	set $maxMCMemberCount, 2;
	set $MCEntryPrice, 0;//5000;
	setarray $MCDuration[0],5,10,15; //minutes range
	set $misterycaseon, 0;
	set .pointPerKill, 10;
	set .pointPerVictory, 100;
	set .maxSpokenCount, 3;
	set .delayBetweenCount, 6; //10s range before new poping
	bindatcmd "mcboard", strnpcinfo(3) + "::OnBoardAsked";
	set .buffs, 1;
	end;
}

prontera,119,166,4	script	Mistery Case::alamcent	734,{
	set .@npcname$, "[Prun]";
	mes .@npcname$;
	mes "Hello to you, " + strcharinfo(0);
	mes "I'm the Mistery Case Manager.";
	mes "When you talk to me, you'll be able to setup or join an existing Mistery Case.";
	next;
	mes .@npcname$;
	if ($misterycaseon == 2)
	{
		goto OnEnterAgain;
	}
	if ($misterycaseon == 1)
	{
		if(callfunc("CheckMCPlayers") == 1)
		{
			next;
			mes .@npcname$;
			mes "Be patient please. The Mistery Case will start soon.";
			close;
		}
		
		mes "So. Are you interested?";
		if (select("Join the currently running Mistery Case","No, thanks") == 1)
		{
			callsub OnJoin, 1;
		}
		else
		{
			goto OnLeave;
		}
	}
	else if ($misterycaseon == 0)
	{
		mes "So. Are you interested?";
		if (select("Start a new Mistery Case","No, thanks") == 1)
		{
			callsub OnJoin, 0;
		}
		else
		{
			goto OnLeave;
		}
	}
	else
	{
		next;
		mes .@npcname$;
		mes "Sorry, it's too late for this battle. Come back later.";
	}
	close;

OnJoin:
	set .@npcname$, "[Prun]";
	set .@mode, getarg(0);
	next;
	mes .@npcname$;
	mes "Sure!";
	mes "It'll cost you " + $MCEntryPrice + "z to enter.";
	if (Zeny < $MCEntryPrice)
	{
		next;
		mes .@npcname$;
		mes "Sorry, but you lack some money...";
		close;
	}
	if (select("Here you are.","I'll get back") == 1)
	{
		next;
		mes .@npcname$;
		set Zeny, Zeny - $MCEntryPrice;
		mes "Allright.";
		mes "The Mistery Case is pretty simple.";
		mes "Kill everyone you'll see. The last one standing will win.";
		mes "You'll sometimes see some Chest poping around the map.";
		mes "The first one to open it will get a Super Power as a reward.";
		mes "The others will get a malus! So it's a double headed axe!";
		next;
		mes .@npcname$;
		mes "The reward will be the total amount paid by all players.";
		mes "I'll just keep a little part for me.";
		mes "The fight will starts when " + $maxMCMemberCount + " members will join.";
		if (.@mode == 0)
		{
			next;
			mes .@npcname$;
			mes "Now, choose a duration.";
			set .@message$, "It can last for ";
			set .@menu$, "";
			for(set .@i,0; .@i<getarraysize($MCDuration); set .@i,.@i+1)
			{
				set .@message$, .@message$ + (.@i == 0 ? "" : (.@i == getarraysize($MCDuration) - 1 ? ", or " : ", ")) + $MCDuration[.@i] + " minutes";
				set .@menu$, .@menu$ + (.@i == 0 ? "" : ":") + $MCDuration[.@i] + " minutes";
			} 
			set .@message$, .@message$ + ".";
			mes .@message$;
			mes "What's your choice?";
			set .@duration, select(.@menu$) - 1;
			next;
			mes .@npcname$;
			mes "Allright, let's go for " + $MCDuration[.@choice] + " minutes then.";
			next;
			mes .@npcname$;
		}
		mes "Good luck.";
		next;
		getmapxy(.@map$, .@x, .@y, 0);
		savepoint(.@map$, .@x, .@y);
		set MCPOINTS, 0;
		warp $mCMap$, 0, 0;
		if (.@mode == 0)
		{
			deletearray $MCMembers[0], getarraysize($MCMembers);
			set $MCMembers[0], getcharid(3);
			
			set $misterycaseon, 1;
			set $battleDuration, $MCDuration[.@choice];
			announce "[Mistery Case] " + strcharinfo(0) + " just join the Mistery Case. Who will come to fight him?", bc_red|bc_all;
		}
		else
		{
			set $MCMembers[getarraysize($MCMembers)], getcharid(3);
			if (getarraysize($MCMembers) < $maxMCMemberCount)
			{
				announce "[Mistery Case] " + strcharinfo(0) + " just join the Mistery Case. Still " + ($maxMCMemberCount - getarraysize($MCMembers)) + " fighters needed to begin!", bc_red|bc_all;
			}
		}
	}
	else
	{
		goto OnLeave;
	}
	close;

OnEnterAgain:
	set .@found, callfunc("CheckMCPlayers");
	if(.@found == 1)
	{
		mes "Get ready, I'll send you back.";
		warp $mCMap$, 0, 0;
	}
	else
	{
		mes "You're not part of this Mistery Case.";
		mes "Wait for another to begin, please.";
	}
	close;

OnLeave:
	set .@npcname$, "[Prun]";
	next;
	mes .@npcname$;
	mes "Your choice!";
	close;
}

function	script	CheckMCPlayers	{
	for(set .@i,0; .@i<getarraysize($MCMembers) && .@found == 0; set .@i,.@i+1)
	{
		if($MCMembers[.@i] == getcharid(3))
		{
			return 1;
		}
	}
	return 0;
}