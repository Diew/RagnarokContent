//===== Alayne Scripts =======================================
//= Hel Realm Instance
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//
//= An instance based over Hel, the Helheim Guardian
//
//===== Todo Remaining: ======================================
//
//===== Additional Comments: =================================
//
//============================================================

prontera,118,160,5	script	Hel Realm::alahren	4_F_CAPEGIRL,{
OnStart:
	set .@party_id, getcharid(1);
	set .@hr_leader, ( getcharid(0) == getpartyleader(.@party_id,2) );

	set $instance_hr_id, -1;
	
	//RATHENA
	set .@has_instance, $instance_hr_id == getcharid(1);
	//HERCULES
	//set .@has_instance, has_instance($hrMap$[0], $instance_hr_id);
	
	set .@instance_delay, ( instance_delay_hr - gettimetick(2) );

	set .@npcname$, "[" + .instance_name$ + "]";
	mes .@npcname$;
	mes "This is the entrance of " + .instance_name$ + ".";
	mes "The more you look at it, the more you feel disturb. Something's wrong happening in there.";
	if( .@instance_delay > 0 )
	{
		set .@hours, .@instance_delay / (60 * 60);
		set .@remaining, .@instance_delay - .@hours * (60 * 60);
		set .@minutes, .@remaining / (60);
		set .@remaining, .@remaining - .@minutes * (60);
		set .@seconds, .@remaining;
		mes "You have to wait : ^FF0000"+ .@hours +" Hours, " + .@minutes + " Minutes and " + .@seconds + " Seconds.^000000";
		//close;
	}
	if (.requiredItem != -1 && countitem(.requiredItem) < 1)
	{
		mes "You decide to step back.";
		mes "At no cost you'll enter in that place without being well prepared.";
		close;
	}
	switch( select( 
		(( .@party_id && .@hr_leader && !.@has_instance )?"Create " + .instance_name$:""),
		(( .@party_id && .@has_instance )?"Enter " + .instance_name$:""),
		(( .@party_id && .@hr_leader && .@has_instance )?"Destroy " + .instance_name$:""),
	"Nevermind."
		)){
	case 1:
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			set .@origin, getcharid(3);
			set .@gettimetick, gettimetick(2);
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set .@member_count, .@member_count + 1;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) set .@fail, .@fail|1;
					if( Zeny < .instance_zeny ) set .@fail, .@fail|2;
					if( instance_delay_hr > .@gettimetick ) set .@fail, .@fail|4;
					if( .@fail ){
						set .@name$, strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		
		//check that nobody else is inside
		for(set .@i,1; .@i<=getarraysize($hrMap$); set .@i,.@i+1)
		{
			if (getmapusers(instance_mapname($hrMap$[.@i], $hr_instance)) > 0) {
				mes "Another team is already in the " + .instance_name$ + ". I can't send you there, sorry.";
				close;
			}
		}
		
		//clean any previously remaining instance
		if($hr_instance != -1)
		{
			instance_destroy $hr_instance;
			set $hr_instance, -1;
			set $instance_hr_id, -1;
		}
		
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Failed ]^000000";
			mes "^0055FF"+.@name$+"^000000, You cannot join right now.";
			mes " ";
			mes "^FF0000[ Reason: ]^000000";
			if( .@fail & 1 ) mes "^777777You must be Level: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777You need "+ .instance_zeny +" Zeny^000000";
			if( .@fail & 4 ) mes "^777777You still have cooldown.^000000";
			//break;
		}
		
		getpartymember getcharid(1),0;
		if (instance_check_party(.@party_id, .min_party_member, .base_level[0], .base_level[1]) == 0) {
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Make or join a party with at least "+.min_party_member+" member and try again, with level between " +.base_level[0] + " and " + .base_level[1] + ".";
		}
		else{
			//RATHENA
			set $hr_instance, instance_create( .instance_name$ );
			//HERCULES
			//set $hr_instance, instance_create(.instance_name$, .@party_id, IOT_PARTY);
			if( $hr_instance < 0 ) {
				npctalk .instance_name$ + " reservation failed.";
				mes "^0000FF"+ .instance_name$ +" ^000000 - Reservation Failed!";
				close;
			}
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "^0000FF"+.instance_name$+" ^000000 - Reserved";
			set .@gettimetick, gettimetick(2);
			getpartymember .@party_id,2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set instance_delay_hr, ( .@gettimetick + ( .instance_delay ));
					set Zeny, Zeny - .instance_zeny;
					dispbottom "["+.instance_name$+"] -"+ .instance_zeny +" Zeny and Instance Cooldown Applied.";
				}
			attachrid( .@origin );
			npctalk .instance_name$ + " has been generated for Party - " + getpartyname( .@party_id );
			
			set $instance_hr_id, getcharid(1);
			
			//HERCULES
			//for( set .@i, 0; .@i < getarraysize($hrMap$); set .@i,.@i+1 )
			//{
			//	if( instance_attachmap( $hrMap$[.@i],$hr_instance ) == "" ){
			//		setmapflag $hrMap$[.@i],mf_zone,"Memorial Dungeon";
			//		npctalk .instance_name$ + " reservation failed due to map attach failure.";
			//		mes "- ^FF0000Reservation Failed!^000000";
			//		instance_destroy( $hr_instance );
			//		set $hr_instance, -1;
			//		set $instance_hr_id, -1;
			//		close;
			//	}
			//}
			//instance_set_timeout .instance_delay,300,$hr_instance;
			//instance_init($hr_instance);
			
			//disable unused npcs for now
			disablenpc instance_npcname( "hrwarp#0", $hr_instance );
 		}
	case 2:
		if( $instance_hr_id != getcharid(1)){
			mes .instance_name$ + " doesn't exist for Party - "+getpartyname( .@party_id );
		}
		else{
			//RATHENA
			switch(instance_enter(.instance_name$)) {
				default:
					mes "An unknown error has occurred.";
					close;
				case 2:
					mes "The memorial dungeon " + .instance_name$ + " does not exist.";
					mes "The party leader did not generate the dungeon yet.";
					close;
				case 1:
					mes "You can enter the dungeon after making the party.";
					close;
				case 0:
					mapannounce $hrMap$[0], strcharinfo(0)+" from party, " +getpartyname( .@party_id )+", lr entering the dungeon, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
					end;
			}
			
			//HERCULES
			//if( has_instance( $hrMap$[0] ) == "" ){
			//	mes "Hel Realm doesnt exist for Party - "+getpartyname( .@party_id );
			//}
			//else{
			//	warp $hrMap$[0],142,56;
			//	instance_announce $hr_instance, getpartyname(getcharid(1))+"  entered in the "+.instance_name$+"!",bc_map,"0x00ff99";
			//}
		}
		break;
	case 3:
		mes .instance_name$ + " Destroyed.";
		instance_destroy $hr_instance;
		set $hr_instance, -1;
		set $instance_hr_id, -1;
		break;
	default: 
		break;
}
close;

OnInit:
	setarray .base_level[0],80,99;
	setarray $hrMap$[0],"1@hr","2@hr";
	set .map_size, getarraysize( $hrMap$ );
	set .min_party_member, 1;
	set .instance_zeny, 0;
	set .instance_delay, 7200;
	set $hr_timer_delay, 3600; //1 hour to clean the instance
	set .instance_name$, "Hel Realm";
	set $hr_instance, -1;
	set $instance_hr_id, -1;
	set .requiredItem, -1; //define the required item to enter in the instance. Leave to -1 to disable
	set $hrTearsId, 6075;
	end;
}


2@hr,101,107,4	script	hel_realm	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
end;

OnInstanceInit:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	
	set $instance_hr_level, 0;
	set .max_hr_level, 4;
	
	set $HelId, -1;
	
	set .timerCount, 0;
	set .wavePop, 0;
	set .waveCount, 0;
	
	set $hrPopingTears, -1;
	set $hrRequiredTears, rand(0,20);
 
  set $hrHermodPop, 0;
  set .hermodCount, 0;
	
	set .popCircleMonsters, 0;
	set .CircleMonstersCount, 0;
	 
	initnpctimer;
	end;

function	summon_normal	{
	set .@map$, getarg(0);
	set .@npcname$, getarg(1);
	set .@increaseFloor, getarg(2);
	
	if(.@increaseFloor == 1)
	{
		$instance_hr_level++;
	}
	
	switch( $instance_hr_level ){
		case 1:
			//Invincible dead monsters
			setarray .@monster,2464;
			setarray .@amount,1;
			setarray .@name$[0],"Tortured Soul";
			setarray .@coordinate[0],113,97,168,27;
			break;
		case 2:
			//dead army monsters
			switch(.waveCount)
			{
				if(getmapxy(.@map$, .@x, .@y, UNITTYPE_MOB, $commander1Id) == 0)
				{
					set $lasthrX, .@x;
					set $lasthrY, .@y;
				}
				if(getmapxy(.@map$, .@x2, .@y2, UNITTYPE_MOB, $commander2Id) == 0)
				{
					set $lasthrX2, .@x2;
					set $lasthrY2, .@y2;
				}
				case 0:
					setarray .@monster,2733,1117,2649;
					setarray .@amount,20,10,5;
					end;
				case 30:
					setarray .@monster,1098,1302,1198;
					setarray .@amount,20,10,10;
					break;
				case 60:
					setarray .@monster,2470,1865;
					setarray .@amount,30,10;
					break;
				case 90:
					setarray .@monster,2472,1504,1870;
					setarray .@amount,20,10,15;
					break;
				case 120:
					setarray .@monster,2283,1297,1503;
					setarray .@amount,20,20,15;
					break;
			}
			set .@monster_size, getarraysize( .@monster );
			for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
				set .@target_amount, ( .@amount[.@i] );
				set .@clabel$, .@npcname$ + "::OnNormalDied";
				areamonster(.@map$, $lasthrX, $lasthrY, $lasthrX, $lasthrY,"--ja--",.@monster[.@i],.@target_amount,.@clabel$);
				areamonster(.@map$, $lasthrX2, $lasthrY2, $lasthrX2, $lasthrY2,"--ja--",.@monster[.@i],.@target_amount,.@clabel$);
			}
			break;
		case 4:
			//Hell Circle Monster
			if(getmapxy(.@map$, .@x, .@y, UNITTYPE_MOB, $HelId) != 0)
			{
				debugmes "not found " + $HelId;
			}
			debugmes "circle: " + .currentCircle;
			switch(.currentCircle)
			{
				case 0:
					//Boredom
					setarray .@monster,3323;
					setarray .@amount,5;
					break;
				case 1:
					//Lust
					setarray .@monster,3331;
					setarray .@amount,5;
					break;
				case 2:
					//Gluttony
					setarray .@monster,3324;
					setarray .@amount,5;
					break;
				case 3:
					//Avarice
					setarray .@monster,3325;
					setarray .@amount,5;
					break;
				case 4:
					//Wrath
					setarray .@monster,3329;
					setarray .@amount,5;
					break;
				case 5:
					//Heresy
					setarray .@monster,3326;
					setarray .@amount,5;
					break;
				case 6:
					//Violence
					setarray .@monster,3332;
					setarray .@amount,5;
					break;
				case 7:
					//Ruse
					setarray .@monster,3330;
					setarray .@amount,5;
					break;
				case 8:
					//Trahison
					setarray .@monster,3327;
					setarray .@amount,5;
					break;
			}
			setarray .@coordinate[0],.@x,.@y,.@x,.@y;
			break;
		default:
			mapannounce .@map$, "[ Hel Realm ] : All Floors has been cleared, congratulation. Champions Arena close in 30 seconds.",bc_map,"0x00FF99";
			sleep2 5000;
			instance_destroy $hr_instance;
			set $hr_instance, -1;
			set $instance_hr_id, -1;
			return;
	}
	if($instance_hr_level != 2)
	{
		set .@monster_size, getarraysize( .@monster );
		for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
			set .@target_amount, ( .@amount[.@i] );
			set .@size, 0;
			set .@clabel$, .@npcname$ + "::OnNormalDied";
			set .@na$, "--ja--";
			if(getarraysize(.@name$) > 0)
			{
				set .@na$, .@name$[.@i];
			}
			//RATHENA
			areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],.@na$,.@monster[.@i],.@target_amount,.@clabel$,.@size);
			copyarray $@tempHelId[0], $@mobId[0], getarraysize($@mobId);
			//HERCULES
			//for( set .@j, 0; .@j < .@target_amount; set .@j,.@j+1 ){
				//set $@tempHelId[.@j], areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],.@na$,.@monster[.@i],1,.@clabel$,.@size);
			//}
		}
	}
	
	if($instance_hr_level == 1)
	{
		for(set .@i,0; .@i<getarraysize($@tempHelId); .@i++)
		{
			//unitskilluseid($@tempHelId[.@i], NPC_INVINCIBLE, 1);
			unitskilluseid($@tempHelId[.@i], 685, 1);
		}
	}
	return;
}

function	summon_guardian	{
	set .@map$, getarg(0);
	set .@npcname$, getarg(1);
	set .@label$, .@npcname$ + "::OnGuardianDied";
			
	switch( $instance_hr_level ){
		case 2:
			//Dead commanders
			setarray .@monster,2473,2474;
			setarray .@coordX[0], 183, 183, 141, 141; //2 x for each mob to define an area
			setarray .@coordY[0], 41, 41, 56, 56; //2 x for each mob to define an area
			break;
		case 3:
			//Baldr
			setarray .@monster,3588;
			setarray .@coordinate[0],175,120,175,120;
			setarray .@name$[0],"Baldr";
			break;
		case 4:
			//Monster Hel
			setarray .@monster,3628;
			setarray .@coordinate[0],80,63,80,63;
			setarray .@name$[0],"Hel";
			break;
		default: 
			mapannounce .@map$,"[ Hel Realm ] : Cleared!! Congratulation!!",bc_map,0x00FF99;
			return;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		if(getarraysize(.@coordX) > 0)
		{
			set .@coordinate[0], .@coordX[.@i * 2];
			set .@coordinate[1], .@coordY[.@i * 2];
			set .@coordinate[2], .@coordX[.@i * 2 + 1];
			set .@coordinate[3], .@coordY[.@i * 2 + 1];
		}
		set .@target_amount, 1;
		set .@na$, "--ja--";
		if(getarraysize(.@name$) > 0)
		{
			set .@na$, .@name$[.@i];
		}
		if($instance_hr_level != 2)
		{
			//RATHENA
			areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],.@na$,.@monster[.@i],.@target_amount,.@label$);
			set $HelId, $@mobId[0];
			setunitdata $HelId, UMOB_HP, 500000;
			//HERCULES
			//set $HelId, areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],.@na$,.@monster[.@i],.@target_amount,.@label$);
			//setunitdata($HelId, UDT_HP, 500000);
		}
		else
		{	
			setd("$commander" + (.@i + 1) + "Id", areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],.@na$,.@monster[.@i],.@target_amount,.@label$));
		}
	}
	return;
}

OnNormalDied:
	set .@map$, instance_mapname( $hrMap$[0], $hr_instance );
	if($instance_hr_level == .max_hr_level)
	{
		set .@map$, instance_mapname( $hrMap$[1], $hr_instance );
	}
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
	
	if( .@mob_dead_num == 0 ){
		detachrid;
		sleep2 500;
		
		if($instance_hr_level == 1)
		{			
			disablenpc instance_npcname( "alahrrs", $hr_instance );
			disablenpc instance_npcname( "alahrrs1", $hr_instance );
			disablenpc instance_npcname( "alahrrs2", $hr_instance );
			disablenpc instance_npcname( "alahrrs3", $hr_instance );
			disablenpc instance_npcname( "alahrrs4", $hr_instance );
			disablenpc instance_npcname( "alahrrs5", $hr_instance );
			disablenpc instance_npcname( "alahrrs6", $hr_instance );
			disablenpc instance_npcname( "alahrrs7", $hr_instance );
			disablenpc instance_npcname( "alahrrs8", $hr_instance );
			disablenpc instance_npcname( "alahrrs9", $hr_instance );
			disablenpc instance_npcname( "alahrrs10", $hr_instance );
			disablenpc instance_npcname( "alahrrs11", $hr_instance );
			disablenpc instance_npcname( "alahrrs12", $hr_instance );
		
			enablenpc instance_npcname( "alahrco", $hr_instance );		
			enablenpc instance_npcname( "alahrco2", $hr_instance );	
			
			instance_announce $hr_instance, "[Hel Realm] Some strange Coffins appeared on the area...", bc_map, 0xFF0000;
			sleep2 500;
		}
	}
	end;
	
OnGuardianDied:
	set .@map$, instance_mapname( $hrMap$[0], $hr_instance );
	if($instance_hr_level == .max_hr_level)
	{
		set .@map$, instance_mapname( $hrMap$[1], $hr_instance );
	}
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnGuardianDied" );
	set .@party_id, getcharid(1);
	
	if( !.@mob_dead_num ){
		if($instance_hr_level == 2)
		{
			set .wavePop, 0;
			
			killmonster .@map$, "All";
			
			instance_announce $hr_instance, "[Hel] Hum...Not so bad...Come find me now. I'm waiting.", bc_map, 0xFF0000;
			
			enablenpc instance_npcname( "alahrhe", $hr_instance );		
		}
		if($instance_hr_level == 3)
		{
			set $hrPopingTears, 0;
			
			disablenpc instance_npcname( "alahrher", $hr_instance );
			enablenpc instance_npcname( "alahrhe2", $hr_instance );	
			enablenpc instance_npcname( "hrwarp#0", $hr_instance );	
		}
		if ($instance_hr_level == .max_hr_level)
		{						
			killmonsterall .@map$, "All";
			enablenpc instance_npcname( "alahrve2", $hr_instance );
			enablenpc instance_npcname( "alahrur2", $hr_instance );
			enablenpc instance_npcname( "alahrsk2", $hr_instance );				
			end;
		}
	}
	end;
	
OnTimer10000:
	//each 10s, increase timer
	set .timerCount, .timerCount + 10;
	if(.timerCount == $hr_timer_delay)
	{
		instance_announce $hr_instance, "[ Hel Realm ] The Dimension collapse! You have to flee, right now!", bc_map, 0xFF0000;
		sleep2 5000;
		instance_destroy $hr_instance;
		set $hr_instance, -1;
		set $instance_hr_id, -1;
		stopnpctimer;
	}
	if(.wavePop == 1)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
		set .waveCount, .waveCount + 10;
		switch(.waveCount)
		{
			case 30:
				instance_announce $hr_instance, "[ Hel Commander ] Mages! Raises!", bc_map, 0xFF0000;
				summon_normal(.@map$, .@npc_name$, 0);
				break;
			case 60:
				instance_announce $hr_instance, "[ Hel Commander ] Riders! To battle!", bc_map, 0xFF0000;
				summon_normal(.@map$, .@npc_name$, 0);
				break;
			case 90:
				instance_announce $hr_instance, "[ Hel Commander ] Knights! To your sword!", bc_map, 0xFF0000;
				summon_normal(.@map$, .@npc_name$, 0);
				break;
			case 120:
				instance_announce $hr_instance, "[ Hel Commander ] Reserve! Come, join us!", bc_map, 0xFF0000;
				summon_normal(.@map$, .@npc_name$, 0);
				break;
		}
	}
	if($hrHermodPop == 1)
	{
		set .hermodCount, .hermodCount + 10;    
		if(.hermodCount == 30)
		{
			set $hrPopingTears, 0;
			enablenpc instance_npcname( "alahrher", $hr_instance );
		}
	}
	if($hrPopingTears == 1)
	{
		set .@map$, instance_mapname( $hrMap$[0], $hr_instance );
		makeitem($hrTearsId, 1, .@map$, rand(151,202), rand(145,95));
	}
	if(.popCircleMonsters == 1)
	{
		set .CircleMonstersCount, .CircleMonstersCount + 10;
		if(.CircleMonstersCount == 60)
		{
			set .currentCircle, .currentCircle + 1;
			//each minutes, pop a CircleMonster
			
			instance_announce $hr_instance, "[ Hel ] My Legion! Come to me!", bc_map, 0xFF0000;
			summon_normal( .@map$, .@npc_name$, 0 );
			set .CircleMonstersCount, 0;
		}
	}
	setnpctimer 0;
	end;
	
OnFirstFloorOn:
	set .@map$, instance_mapname( $hrMap$[0], $hr_instance );
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	//pop monsters
	summon_normal( .@map$,.@npc_name$, 1 );
	instance_announce $hr_instance, "[Hel Realm] You're now on the entrance of Helheim.", bc_map, 0xFF0000;
	sleep2 500;
	instance_announce $hr_instance, "[Hel Realm] The monsters here are not really dead yet. You can't simply kill them.", bc_map, 0xFF0000;	
	sleep2 500;
	instance_announce $hr_instance, "[Hel Realm] You'll have to help them to pass away completely.", bc_map, 0xFF0000;	
	end;
	
OnSecondFloorCheck:
	if($coffin1Ready == 1 && $coffin2Ready == 1)
	{
		//both coffin ready
		goto OnSecondFloorOn;
	}
	end;
	
OnSecondFloorOn:	
	set .@map$, instance_mapname( $hrMap$[0], $hr_instance );
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	disablenpc instance_npcname( "alahrco", $hr_instance );
	disablenpc instance_npcname( "alahrco2", $hr_instance );
	instance_announce $hr_instance, "[Urd] Those monsters look way more stronger than the others ones...", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $hr_instance, "[Skuld] Thanks gods, they don't seems to be able to move...", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $hr_instance, "[Urd] Hu hu...You speak a bit quickly, no?", bc_map, 0x00FF00;
	sleep 1500;
	set .wavePop, 1;
	$instance_hr_level++;
	summon_guardian( .@map$, .@npc_name$ );
	summon_normal( .@map$, .@npc_name$, 0 );
	end;
	
OnThirdFloorOn:
	$instance_hr_level++;
	set .@map$, instance_mapname( $hrMap$[0], $hr_instance );
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	summon_guardian( .@map$, .@npc_name$ );
	end;
	
OnFourthFloorOn:
	$instance_hr_level++;
	set .currentCircle, 0;
	set .@map$, instance_mapname( $hrMap$[1], $hr_instance );
	set .@npc_name$, instance_npcname( strnpcinfo(0), $hr_instance );
	summon_guardian( .@map$, .@npc_name$ );
	summon_normal( .@map$, .@npc_name$, 0 );
	set .popCircleMonsters, 1;
	end;
}

1@hr,183,41,3	script	Coffin#2473::alahrco	4_NFCOFFIN,{
	mes "[Coffin]";
	mes "There's a strange coffin in the middle of the room.";
	set .@leader, ( getcharid(0) == getpartyleader(getcharid(1),2) );
	if(.@leader == 1)
	{
		mes "You can heard a whisper coming from it.";
		mes "";
		mes "";
		mes "You have to decide who must die, so we can raise.";
		mes "Choose wisely...";
		getpartymember(getcharid(1), 2);
		set .@partyCount, $@partymembercount;
		copyarray .@members$[0], $@partymembername$[0], .@partyCount;
		copyarray .@membersAId[0], $@partymemberaid[0], .@partyCount;
		set .@menu$, "";
		for(set .@i, 0; .@i<.@partyCount; set .@i,.@i+1)
		{
			set .@menu$, .@menu$ + (.@i == 0 ? "" : ":") + .@members$[.@i];
		}
		set .@choice, select(.@menu$) - 1;
		if(attachrid(.@membersAId[.@choice]) == 1)
		{
			set $coffin1Ready, 1;
			donpcevent instance_npcname( "hel_realm", $hr_instance ) + "::OnSecondFloorCheck";
			specialeffect 216;
			setnpcdisplay strnpcinfo(3), atoi(strnpcinfo(2));
		}
		else
		{
			next;
			mes "[Coffin]";
			mes "You shouldn't involve those who aren't.";
			percentheal -100, -100;
			specialeffect 216;
		}
	}
	close;
	
OnInstanceInit:
OnDisable:
	set $coffin1Ready, 0;
	disablenpc strnpcinfo(3);
	end;
}

1@hr,141,56,3	script	Coffin#2474::alahrco2	4_NFCOFFIN,{
	mes "[Coffin]";
	mes "There's a strange coffin in the middle of the room.";
	set .@leader, ( getcharid(0) == getpartyleader(getcharid(1),2) );
	if(.@leader == 1)
	{
		mes "You can heard a whisper coming from it.";
		mes "";
		mes "";
		mes "You have to decide who must die, so we can raise.";
		mes "Choose wisely...";
		getpartymember(getcharid(1), 2);
		set .@partyCount, $@partymembercount;
		copyarray .@members$[0], $@partymembername$[0], .@partyCount;
		copyarray .@membersAId[0], $@partymemberaid[0], .@partyCount;
		set .@menu$, "";
		for(set .@i, 0; .@i<.@partyCount; set .@i,.@i+1)
		{
			set .@menu$, .@menu$ + (.@i == 0 ? "" : ":") + .@members$[.@i];
		}
		set .@choice, select(.@menu$) - 1;
		if(attachrid(.@membersAId[.@choice]) == 1)
		{
			set $coffin2Ready, 1;
			donpcevent instance_npcname( "hel_realm", $hr_instance ) + "::OnSecondFloorCheck";
			specialeffect 216;
			setnpcdisplay strnpcinfo(3), atoi(strnpcinfo(2));
		}
		else
		{
			next;
			mes "[Coffin]";
			mes "You shouldn't involve those who aren't.";
			percentheal -100, -100;
			specialeffect 216;
		}
	}
	close;
	
OnInstanceInit:
OnDisable:
	set $coffin2Ready, 0;
	disablenpc strnpcinfo(3);
	end;
}

1@hr,72,41,3	script	Soul Flame#0::alahrrs	CLEAR_NPC,4,4,{
	mes "[Soul Flame]";
	mes "There's a strange flame here.";
	mes "You can feel the urge to put your hand in it.";
	mes "Would you like to do it? It looks painfull, just to look at it.";
	if(select("Yes","No") == 1)
	{
		next;
		mes "[Soul Flame]";
		percentheal -30, 0;
		mes "The Flame is now filled with your blood.";
		mes "The deads start howling";
		for(set .@i,0; .@i<getarraysize($@tempHelId); .@i++)
		{
			//unitskilluseid($@tempHelId[.@i], NPC_INVINCIBLEOFF, 1);
			unitskilluseid($@tempHelId[.@i], 686, 1);
		}
		addtimer 15000, strnpcinfo(3) + "::OnInvicibleAgain";
		hideonnpc strnpcinfo(3);
	}
	close;
	
OnInvicibleAgain:
	instance_announce $hr_instance, "[ Hel Realm ] You can hear a tearing cry...", bc_map, 0xFF0000;
	for(set .@i,0; .@i<getarraysize($@tempHelId); .@i++)
	{
		//unitskilluseid($@tempHelId[.@i], NPC_INVINCIBLEOFF, 1);
		unitskilluseid($@tempHelId[.@i], 686, 1);
	}
	hideoffnpc strnpcinfo(3);
	end;
	
OnTouch:	
	dispbottom "You can feel a tiny air breeze coming from somewhere around here.";
	end;
	
OnInstanceInit:
OnEnable:
	enablenpc strnpcinfo(3);
	end;
OnDisable:
	disablenpc strnpcinfo(3);
	end;
}

1@hr,156,13,3	duplicate(alahrrs)	Soul Flame#1::alahrrs1	CLEAR_NPC,4,4
1@hr,240,55,3	duplicate(alahrrs)	Soul Flame#2::alahrrs2	CLEAR_NPC,4,4
1@hr,254,52,3	duplicate(alahrrs)	Soul Flame#3::alahrrs3	CLEAR_NPC,4,4
1@hr,268,153,3	duplicate(alahrrs)	Soul Flame#4::alahrrs4	CLEAR_NPC,4,4
1@hr,268,209,3	duplicate(alahrrs)	Soul Flame#5::alahrrs5	CLEAR_NPC,4,4
1@hr,86,181,3	duplicate(alahrrs)	Soul Flame#6::alahrrs6	CLEAR_NPC,4,4
1@hr,114,111,3	duplicate(alahrrs)	Soul Flame#7::alahrrs7	CLEAR_NPC,4,4
1@hr,158,101,3	duplicate(alahrrs)	Soul Flame#8::alahrrs8	CLEAR_NPC,4,4
1@hr,194,101,3	duplicate(alahrrs)	Soul Flame#9::alahrrs9	CLEAR_NPC,4,4
1@hr,194,137,3	duplicate(alahrrs)	Soul Flame#10::alahrrs10	CLEAR_NPC,4,4
1@hr,158,137,3	duplicate(alahrrs)	Soul Flame#11::alahrrs11	CLEAR_NPC,4,4
1@hr,194,101,3	duplicate(alahrrs)	Soul Flame#12::alahrrs12	CLEAR_NPC,4,4

1@hr,175,120,3	script	Hel::alahrhe	3628,4,4,{
	end;
	
OnTouch:	
	if(.talking == 0)
	{
		set $hrTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "",2;
		npctalk "You're stronger than you first look...";
		instance_announce $hr_instance, "[Hel] You're stronger than you first look...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "This is my place, my kingdom.";
		instance_announce $hr_instance, "[Hel] This is my place, my kingdom.", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "I've been sent here by my father, Loki, after Thor, his brother, burned my face in half.";
		instance_announce $hr_instance, "[Hel] I've been sent here by my father, Loki, after Thor, his brother, burned my face in half.", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "But except for him, I'm not a bad ruler.";
		instance_announce $hr_instance, "[Hel] But except for him, I'm not a bad ruler.", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "Let's ask that to my Guardian, Baldr.";
		instance_announce $hr_instance, "[Hel] Let's ask that to my Guardian, Baldr.", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "If you can defeat him, I'll accept you to go further. Otherwise, you'll better get back.";
		instance_announce $hr_instance, "[Hel] If you can defeat him, I'll accept you to go further. Otherwise, you'll better get back..", bc_map, 0x00FF00;
		sleep2 500;
		set $hrHermodPop, 1;	
		donpcevent instance_npcname( "hel_realm", $hr_instance ) + "::OnThirdFloorOn";
		disablenpc strnpcinfo(3);
	}
	end;
 
OnInstanceInit:	
OnDisable:
	disablenpc strnpcinfo(3);
	set .talking, 0;
	end;
OnEnable:
	enablenpc strnpcinfo(3);
	end;
}

1@hr,176,114,5	script	Hermod::alahrher	4_M_SURA,2,2,{
	//cutin "shaloshi01",2;
	end;
		
OnTouchNPC:		
	if($hrPopingTears == 2)
	{
		npctalk "Baldr, my brother! You have to wake up!";
		set .@hp, getunitdata($HelId, UDT_HP);
		set .@mhp, getunitdata($HelId, UDT_MAXHP);
		if(.@hp <= .@mhp * 30 / 100)
		{
			specialeffect 245;
			sleep2 500;
			npctalk "Good! Come back, brother!";
			killmonster strnpcinfo(4), "All";
			
			set $hrPopingTears, 3;
		}
		else
		{
			npctalk "It doesn't work. You have to weaken him more than that!";
			set $hrPopingTears, 1;
		}
	}
	end;
	
OnTouch:
	if($hrPopingTears <= 0)
	{
		npctalk "Baldr, my brother! You don't want to do that! You're supposed to join us for the Ragnarok, not Loki's side.";
		sleep2 500;
		npctalk "No way, he does't hear me...Bring me those tears falling from the skies. That's everybody's tears.";
		sleep2 500;
		npctalk "Baldr was the nicest of us all, everyong weeping him. It'll wake him up, I'm sure!";
		set $hrPopingTears, 1;
	}
	else if($hrPopingTears == 1)
	{
		if(countitem($hrTearsId) >= $hrRequiredTears)
		{
			delitem $hrTearsId, $hrRequiredTears;
			npctalk "Good, now lure him to me!";
			set $hrPopingTears, 2;
		}
		else
		{
			npctalk "Bring me those tears!";
		}
	}
	else if($hrPopingTears == 3)
	{
		npctalk "Thank you adventurer. Thanks to you Baldr is now awaken.";
		sleep2 500;
		npctalk "I can't do much for you, but take this as a reward.";
		getitem 7539, 15;
		
		disablenpc instance_npcname( "alahrher", $hr_instance );
		enablenpc instance_npcname( "alahrhe2", $hr_instance );	
		enablenpc instance_npcname( "hrwarp#0", $hr_instance );	
	}
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

2@hr,80,63,3	script	Hel::alahrhe2	3628,4,4,{
	end;
	
OnTouch:	
	if(.talking == 0)
	{
		set $hrTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "",2;
		npctalk "Hum...Apparently, Baldr estimate you enough to make you pass...";
		instance_announce $hr_instance, "[Hel] Hum...Apparently, Baldr estimate you enough to make you pass...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "What? Did you really think you defeated him? Poor fools...";
		instance_announce $hr_instance, "[Hel] What? Did you really think you defeated him? Poor fools...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "Allright, let's fight then. You've reached the place where I rule. You deserve that.";
		instance_announce $hr_instance, "[Hel] Allright, let's fight then. You've reached the place where I rule. You deserve that.", bc_map, 0x00FF00;
		sleep2 500;
			
		donpcevent instance_npcname( "hel_realm", $hr_instance ) + "::OnFourthFloorOn";
		disablenpc strnpcinfo(3);
	}
	end;
	
OnInstanceInit:	
OnDisable:
	disablenpc strnpcinfo(3);
	set .talking, 0;
	end;
OnEnable:
	enablenpc strnpcinfo(3);
	end;
}

1@hr,141,65,3	script	Verdandi::alahrve	4_F_NYDHOG,2,2,{
	//cutin "ep14_nyd01",2;
	end;
		
OnTouch:	
	if(.talking == 0)
	{
		set $hrTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "ep14_nyd01",2;
		npctalk "Brrr...I've always hated Helheim...";
		instance_announce $jl_instance, "[Verdandi] Brrr...I've always hated Helheim...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "Muspelheim is hot, Jotunheim is cold...";
		instance_announce $jl_instance, "[Verdandi] Muspelheim is hot, Jotunheim is cold...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "Even Niflheim is...'purest' than Helheim. This is the place of lost and wandering souls...";
		instance_announce $jl_instance, "[Verdandi] Even Niflheim is...'purest' than Helheim. This is the place of lost and wandering souls...", bc_map, 0x00FF00;
		sleep2 500;
	
		donpcevent instance_npcname( "alahrsk", $hr_instance ) + "::OnDialog1_2";
	}
	end;

OnInstanceInit:
	set .talking, 0;
	enablenpc strnpcinfo(3);
	end;
}

1@hr,138,68,5	script	Urd::alahrur	4_F_PINKWOMAN,{
	//cutin "bu_maggi1",0;
	end;
		
OnDialog1_3:			
	attachrid($hrTalkingId);
	cutin "bu_maggi1", 2;
	npctalk "On the other hand, Deads will obey to their master, Hel, whatever happens...";
	instance_announce $hr_instance, "[Urd] On the other hand, Deads will obey to their master, Hel, whatever happens...", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "I don't want to get any further in.";
	instance_announce $hr_instance, "[Urd] I don't want to get any further in.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "You'll have to go without us.";
	instance_announce $hr_instance, "[Urd] You'll have to go without us.", bc_map, 0x00FF00;
	sleep2 500;
	charcommand "#me " + strcharinfo(0) + " Not that I'm not used to it...";
	instance_announce $hr_instance, "[" + strcharinfo(0) + "] Not that I'm not used to it...", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "Well...Anyway. Let's meet a bit further. And be careful, Deads are dangerous.";
	instance_announce $hr_instance, "[Urd] Well...Anyway. Let's meet a bit further. And be careful, Deads are dangerous.", bc_map, 0x00FF00;
	sleep2 500;
	
	cutin "", 255;
	
	disablenpc strnpcinfo(3);
	disablenpc instance_npcname( "alahrve", $hr_instance );
	disablenpc instance_npcname( "alahrsk", $hr_instance );
	
	donpcevent instance_npcname( "hel_realm", $hr_instance ) + "::OnFirstFloorOn";
	end;	
	
OnInstanceInit:
	enablenpc strnpcinfo(3);
	end;
}

1@hr,144,67,5	script	Skuld::alahrsk	4_F_SHALOSH,{
	//cutin "shaloshi01",2;
	end;
		
OnDialog1_2:			
	attachrid($hrTalkingId);
	cutin "shaloshi01", 2;
	npctalk "I know what you mean. Deads are just...dead. Demons are commonly known as bad.";
	instance_announce $hr_instance, "[Skuld] I know what you mean. Deads are just...dead. Demons are commonly known as bad.", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alahrur", $hr_instance ) + "::OnDialog1_3";
	end;
	
OnInstanceInit:
	enablenpc strnpcinfo(3);
	end;
}

2@hr,79,56,3	script	Verdandi::alahrve2	4_F_NYDHOG,2,2,{
	//cutin "ep14_nyd01",2;
	end;
		
OnTouch:	
	if(.talking == 0)
	{
		set $hrTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "ep14_nyd01",2;
		npctalk "Congratulations! You've defeated Hel, the Hell's Guardian.";
		instance_announce $hr_instance, "[Verdandi] Congratulations! You've defeated Hel, the Hell's Guardian.", bc_map, 0x00FF00;
		sleep2 500;
	
		donpcevent instance_npcname( "alahrur2", $hr_instance ) + "::OnDialog2_2";
	}
	end;

OnInstanceInit:
	set .talking, 0;
	disablenpc strnpcinfo(3);
	end;
}

2@hr,75,58,5	script	Urd::alahrur2	4_F_PINKWOMAN,{
	//cutin "bu_maggi1",0;
	end;
		
OnDialog2_2:			
	attachrid($hrTalkingId);
	cutin "bu_maggi1", 2;
	npctalk "You know, it's not a small victory. She's very powerfull...";
	instance_announce $hr_instance, "[Urd] You know, it's not a small victory. She's very powerfull...", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alahrsk2", $hr_instance ) + "::OnDialog2_3";
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

2@hr,83,58,5	script	Skuld::alahrsk2	4_F_SHALOSH,{
	//cutin "shaloshi01",2;
	end;
		
OnDialog2_3:			
	attachrid($hrTalkingId);
	cutin "shaloshi01", 2;
	npctalk "Anyway, there's still plenty of Ases and Giants, right?";
	instance_announce $hr_instance, "[Skuld] Anyway, there's still plenty of Ases and Giants, right?", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "You should be prepared for the next one you'll encounter.";
	instance_announce $hr_instance, "[Skuld] You should be prepared for the next one you'll encounter.", bc_map, 0x00FF00;
	sleep2 5000;
	instance_destroy( $hr_instance );
	set $hr_instance, -1;
	set $instance_hr_id, -1;
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

1@hr,175,120	warp	hrwarp#0	2,2,2@hr,36,22

