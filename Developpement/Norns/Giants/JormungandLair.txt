//===== Alayne Scripts =======================================
//= Jormungand Lair Instance
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//
//= An instance based over Jormungand, the World Snake
//
//===== Todo Remaining: ======================================
//
//===== Additional Comments: =================================
//
//============================================================

prontera,142,182,5	script	Jormungand Lair::alajlen	4_F_ALCHE_A,{
OnStart:
	set .@party_id, getcharid(1);
	set .@jl_leader, ( getcharid(0) == getpartyleader(.@party_id,2) );

	set $instance_jl_id, -1;
	
	//RATHENA
	set .@has_instance, $instance_jl_id == getcharid(1);
	//HERCULES
	//set .@has_instance, has_instance(.map$[0], $jl_instance);
	
	set .@instance_delay, ( instance_delay_jl - gettimetick(2) );

	set .@npcname$, "[" + .instance_name$ + "]";
	mes .@npcname$;
	mes "This is the entrance of " + .instance_name$ + ".";
	mes "The more you look at it, the more you feel disturb. Something's wrong happening in there.";
	next;
	if( .@instance_delay > 0 )
	{
		set .@hours, .@instance_delay / (60 * 60);
		set .@remaining, .@instance_delay - .@hours * (60 * 60);
		set .@minutes, .@remaining / (60);
		set .@remaining, .@remaining - .@minutes * (60);
		set .@seconds, .@remaining;
		mes "You have to wait : ^FF0000"+ .@hours +" Hours, " + .@minutes + " Minutes and " + .@seconds + " Seconds.^000000";
		//close;
	}
	if (.requiredItem != -1  && countitem(.requiredItem) < 1)
	{
		mes "You decide to step back.";
		mes "At no cost you'll enter in that place without being well prepared.";
		close;
	}
	switch( select( 
		(( .@party_id && .@jl_leader && !.@has_instance )?"Create " + .instance_name$:""),
		(( .@party_id && .@has_instance )?"Enter " + .instance_name$:""),
		(( .@party_id && .@jl_leader && .@has_instance )?"Destroy " + .instance_name$:""),
	"Nevermind."
		)){
	case 1:
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			set .@origin, getcharid(3);
			set .@gettimetick, gettimetick(2);
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set .@member_count, .@member_count + 1;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) set .@fail, .@fail|1;
					if( Zeny < .instance_zeny ) set .@fail, .@fail|2;
					if( instance_delay_jl > .@gettimetick ) set .@fail, .@fail|4;
					if( .@fail ){
						set .@name$, strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		
		//check that nobody else is inside
		for(set .@i,1; .@i<=getarraysize(.map$); set .@i,.@i+1)
		{
			if (getmapusers(instance_mapname(.map$[.@i], $jl_instance)) > 0) {
				mes "Another team is already in the " + .instance_name$ + ". I can't send you there, sorry.";
				close;
			}
		}
		
		//clean any previously remaining instance
		if($jl_instance != -1)
		{
			instance_destroy $jl_instance;
			set $jl_instance, -1;
			set $instance_jl_id, -1;
		}
		
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Failed ]^000000";
			mes "^0055FF"+.@name$+"^000000, You cannot join right now.";
			mes " ";
			mes "^FF0000[ Reason: ]^000000";
			if( .@fail & 1 ) mes "^777777You must be Level: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777You need "+ .instance_zeny +" Zeny^000000";
			if( .@fail & 4 ) mes "^777777You still have cooldown.^000000";
			//break;
		}
		
		getpartymember getcharid(1),0;
		if (instance_check_party(.@party_id, .min_party_member, .base_level[0], .base_level[1]) == 0) {
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Make or join a party with at least "+.min_party_member+" member and try again, with level between " +.base_level[0] + " and " + .base_level[1] + ".";
		}
		else{
			//RATHENA
			set $jl_instance, instance_create( .instance_name$ );
			//HERCULES
			//set $jl_instance, instance_create(.instance_name$, .@party_id, IOT_PARTY);
			if( $jl_instance < 0 ) {
				npctalk .instance_name$ + " reservation failed.";
				mes "^0000FF"+ .instance_name$ +" ^000000 - Reservation Failed!";
				close;
			}
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "^0000FF"+.instance_name$+" ^000000 - Reserved";
			set .@gettimetick, gettimetick(2);
			getpartymember .@party_id,2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set instance_delay_jl, ( .@gettimetick + ( .instance_delay ));
					set Zeny, Zeny - .instance_zeny;
					dispbottom "["+.instance_name$+"] -"+ .instance_zeny +" Zeny and Instance Cooldown Applied.";
				}
			attachrid( .@origin );
			npctalk .instance_name$ + " has been generated for Party - " + getpartyname( .@party_id );
			
			set $instance_jl_id, getcharid(1);
			
			//HERCULES
			//for( set .@i, 0; .@i < getarraysize(.map$); set .@i,.@i+1 )
			//{
			//	if( instance_attachmap( .map$[.@i],$jl_instance ) == "" ){
			//		setmapflag .map$[.@i],mf_zone,"Memorial Dungeon";
			//		npctalk .instance_name$ + " reservation failed due to map attach failure.";
			//		mes "- ^FF0000Reservation Failed!^000000";
			//		instance_destroy( $jl_instance );
			//		set $jl_instance, -1;
			//		set $instance_jl_id, -1;
			//		close;
			//	}
			//}
			//instance_set_timeout .instance_delay,300,$jl_instance;
			//instance_init($jl_instance);
			
			//disable unused npcs for now
 		}
	case 2:
		if( $instance_jl_id != getcharid(1)){
			mes .instance_name$ + " doesn't exist for Party - "+getpartyname( .@party_id );
		}
		else{
			//RATHENA
			switch(instance_enter(.instance_name$)) {
				default:
					mes "An unknown error has occurred.";
					close;
				case 2:
					mes "The memorial dungeon " + .instance_name$ + " does not exist.";
					mes "The party leader did not generate the dungeon yet.";
					close;
				case 1:
					mes "You can enter the dungeon after making the party.";
					close;
				case 0:
					mapannounce .map$[0], strcharinfo(0)+" from party, " +getpartyname( .@party_id )+", lr entering the dungeon, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
					end;
			}
			
			//HERCULES
			//if( has_instance( .map$[0] ) == "" ){
			//	mes "Jormungand Lair doesnt exist for Party - "+getpartyname( .@party_id );
			//}
			//else{
			//	warp .map$[0],248,278;
			//	instance_announce $jl_instance, getpartyname(getcharid(1))+"  entered in the "+.instance_name$+"!",bc_map,"0x00ff99";
			//}
		}
		break;
	case 3:
		mes .instance_name$ + " Destroyed.";
		instance_destroy $jl_instance;
		set $jl_instance, -1;
		set $instance_jl_id, -1;
		break;
	default: 
		break;
}
close;

OnInit:
	setarray .base_level[0],80, 99;
	setarray .map$[0],"1@jl";
	set .map_size, getarraysize( .map$ );
	set .min_party_member, 1;
	set .instance_zeny, 0;
	set .instance_delay, 7200;
	set $jl_timer_delay, 3600; //1 hour to clean the instance
	set .instance_name$, "Jormungand Lair";
	set $jl_instance, -1;
	set $instance_jl_id, -1;
	set .requiredItem, -1; //define the required item to enter in the instance. Leave to -1 to disable
	set $jlRibId, 6598;
	set $jlScaleId, 6603;
	set $jlRuneId, 6604;
	end;
}


1@jl,101,107,4	script	jormungand_lair	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
end;

OnInstanceInit:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	
	set $instance_jl_level, 0;
	set .max_jl_level, 5;
	
	set $jormungandId, -1;
	
	set .timerCount, 0;
	set $jlContinuousPop, 0;
	set .weakenedCount, 0;
	set $jlUnweakenedPop, -1;
	set $jorKilled, 0;
	 
	initnpctimer;
	end;

function	summon_normal	{
	set .@map$, getarg(0);
	set .@npcname$, getarg(1);
	set .@increaseFloor, getarg(2);
	
	if(.@increaseFloor == 1)
	{
		$instance_jl_level++;
	}
	
	switch( $instance_jl_level ){
		case 1:
			//Snake monsters
			setarray .@monster,1029,2790,1993,2653,2205;
			setarray .@amount,40,10,20,5,1;
			setarray .@coordinate,72,309,320,197;
			break;
		case 2:
			//support monsters
			setarray .@monster,1993;
			set .@mob_dead_num, mobcount(.@map$,.@npcname$+"::OnNormalDied" );
			//repop the max amount of monsters
			setarray .@amount,20 - .@mob_dead_num;
			setarray .@coordinate,283,206,232,253;
			break;
		case 3:
			//Rune stone guardians
			setarray .@monster,2205;
			setarray .@amount,10;
			setarray .@coordinate,$jlx,$jly,$jlx,$jly;
			break;
		case 5:
			//Snake monsters, poped after each Jor part is killed
			setarray .@monster,2790;
			setarray .@amount,10;
			setarray .@coordinate,226,223,166,187;
			break;
		default:
			mapannounce .@map$, "[ Jormungand Lair ] : All Floors has been cleared, congratulation. Champions Arena close in 30 seconds.",bc_map,"0x00FF99";
			sleep2 5000;
			instance_destroy $jl_instance;
			set $jl_instance, -1;
			set $instance_jl_id, -1;
			return;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, ( .@amount[.@i] );
		set .@size, 0;
		if(getarraysize(.@label$) == 0)
		{
			set .@clabel$, .@npcname$ + "::OnNormalDied";
		}
		else
		{
			set .@clabel$, .@npcname$ + "::" + .@label$[.@i];
		}	
		if(getarraysize(.@coordX) > 0)
		{
			set .@coordinate[0], .@coordX[.@i * 2];
			set .@coordinate[1], .@coordY[.@i * 2];
			set .@coordinate[2], .@coordX[.@i * 2 + 1];
			set .@coordinate[3], .@coordY[.@i * 2 + 1];
		}
		areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@clabel$,.@size;
	}
	return;
}

function	summon_guardian	{
	set .@map$, getarg(0);
	set .@npcname$, getarg(1);
	set .@label$, .@npcname$ + "::OnGuardianDied";
				
	switch( $instance_jl_level ){
		case 2:
			//Weakened Jormungand
			setarray .@monster,2535;
			setarray .@coordinate,264,230,264,230;
			break;
		case 4:
			//Human Jormungand
			setarray .@monster,3327;
			setarray .@coordinate,183,58,196,46;
			break;
		case 5:
			//Monster Jormungand
			setarray .@monster,3273;
			setarray .@coordinate,183,58,196,46;
			break;
		default: 
			mapannounce .@map$,"[ Jormungand Lair ] : Cleared!! Congratulation!!",bc_map,0x00FF99;
			return;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, 1;
		set .@size, 0;
		//RATHENA
		areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"Jormungand",.@monster[.@i],.@target_amount,.@label$,.@size);
		set $jormungandId, $@mobId[0];
		//HERCULES
		//set $jormungandId, areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"Jormungand",.@monster[.@i],.@target_amount,.@label$,.@size);
	}
	
	if($jlUnweakenedPop == 1)
	{
		//unitskilluseid($jormungandId, NPC_INVINCIBLE, 1);
		unitskilluseid($jormungandId, 685, 1);
	}
	if($instance_jl_level == 2)
	{
		//RATHENA
		getunitdata($jormungandId, .@jormData);
		setunitdata $jormungandId, 2, .@jormData[UMOB_MAXHP] / 3;
		//HERCULES
		//set .@mhp, getunitdata($jormungandId, UDT_MAXHP);		
		//setunitdata($jormungandId, UDT_HP, .@mhp / 3);
	}
	else if($instance_jl_level == 5)
	{
		setarray .@monster,3253;
		setarray .@coordinate,226,223,166,187;
		
		//RATHENA
		getunitdata($jormungandId, .@jormData);
		set .@mhp, .@jormData[UMOB_MAXHP];
		//HERCULES
		//set .@mhp, getunitdata($jormungandId, UDT_MAXHP);
		getpartymember .@party_id,2;
		set .@memberCount, $@partymembercount;
		
		if(.@memberCount > getarraysize(.@monster))
			set .@memberCount, getarraysize(.@monster);
		
		//RATHENA
		setunitdata $jormungandId, 2, .@mhp / (.@memberCount + 1);
		//HERCULES
		//setunitdata($jormungandId, UDT_HP, .@mhp / (.@memberCount + 1));
		
		set .@label$, .@npcname$ + "::OnGuardianSecondaryDied";
		
		//RATHENA
		areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"Jormungand",.@monster[.@i],.@memberCount,.@label$,.@size);
		copyarray .@tempJorId[0], $@mobId[0], getarraysize($@mobId);
		//HERCULES
		//set .@tempJorId, areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"Jormungand",.@monster[.@i],.@memberCount,.@label$,.@size);
		
		for(set .@i,0; .@i<getarraysize(.@tempJorId); .@i++)
		{
			//RATHENA
			setunitdata .@tempJorId, 2, .@mhp / (.@memberCount + 1);
			//HERCULES
			//setunitdata(.@tempJorId, UDT_HP, .@mhp / (.@memberCount + 1));
		}
	}
	return;
}

OnNormalDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
	
	if($instance_jl_level == 1)
	{
		if(rand(0,1) == 0)
		{
			getitem $jlRibId, 1;
		}
		else
		{
			getitem $jlScaleId, 1;
		}
	}
	else if($instance_jl_level == 3)
	{
		if(rand(0,3) == 0)
		{
			getitem $jlRuneId, 1;
		}
	}
	
	if( .@mob_dead_num == 0 ){		
		if($instance_jl_level == 1)
		{
			if($jlContinuousPop == 1)
			{
				summon_normal( .@map$,.@npc_name$, 0 );
			}
		}
		else if($instance_jl_level == 2 && $jlUnweakenedPop == 1)
		{
			//unitskilluseid($jormungandId, NPC_INVINCIBLEOFF, 1);
			unitskilluseid($jormungandId, 685, 1);
			instance_announce $jl_instance, "[Verdandi] He's vulnerable now! Kill it!", bc_map, 0x00FF00;
		}
	}
	end;
	
OnGuardianDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnGuardianDied" );
	set .@party_id, getcharid(1);
	
	if( !.@mob_dead_num ){
		if($instance_jl_level == 2)
		{
			set $jorKilled, 1;
			
			killmonsterall .@map$, "All";
			enablenpc instance_npcname( "alajlt", $jl_instance );
			enablenpc instance_npcname( "alajlve2", $jl_instance );
			enablenpc instance_npcname( "alajlur2", $jl_instance );
			enablenpc instance_npcname( "alajlsk2", $jl_instance );	
		}
		if ($instance_jl_level == .max_jl_level)
		{			
			set $jorKilled, 1;
			
			killmonsterall .@map$, "All";
			enablenpc instance_npcname( "alajlve3", $jl_instance );
			enablenpc instance_npcname( "alajlur3", $jl_instance );
			enablenpc instance_npcname( "alajlsk3", $jl_instance );				
			end;
		}
		
		sleep2 5000;
		summon_normal( .@map$,.@npc_name$ );
	}
	end;
	
OnGuardianSecondaryDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnGuardianSecondaryDied" );
	instance_announce $jl_instance, "[ Jormungand ] Hisssssssssss! My body!!!!", bc_map, 0xFF0000;
	if( .@mob_dead_num == 0 ){
		summon_normal( .@map$, .@npc_name$, 0 );
		sleep2 500;
		instance_announce $jl_instance, "[ Jormungand ] I will devour you with pleasssure!!!!", bc_map, 0xFF0000;
	}
	else
	{
		summon_normal( .@map$, .@npc_name$, 0 );
	}
	end;
	
OnTimer10000:
	//each 10s, increase timer
	set .timerCount, .timerCount + 10;
	if(.timerCount == $jl_timer_delay)
	{
		instance_announce $jl_instance, "[ Jormungand Lair ] The Dimension collapse! You have to flee, right now!", bc_map, 0xFF0000;
		sleep2 5000;
		instance_destroy $jl_instance;
		set $jl_instance, -1;
		set $instance_jl_id, -1;
		stopnpctimer;
	}
	
	if($instance_jl_level == 2 && $jorKilled == 0)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
		
		if($jlUnweakenedPop == 1)
		{
			if(.weakenedCount == 0)
			{
				instance_announce $jl_instance, "[Urd] Looks like you can't touch it...I need to hurry!", bc_map, 0xFF0000;
				sleep2 500;		
				instance_announce $jl_instance, "[Urd] Try to kill his support while waiting for me!", bc_map, 0xFF0000;
				sleep2 500;				
			}
			else if(.weakenedCount == 30 || .weakenedCount == 60)
			{
				//NPC_INVINCIBLE
				unitskilluseid($jormungandId, 685, 1);
			}
		}
		set .weakenedCount, .weakenedCount + 10;
		if(.weakenedCount == 60)
		{
			if($jlUnweakenedPop == 1)
			{
				//after 1 minute delay, start poping thor
				instance_announce $jl_instance, "[Urd] I've found it in the Past. Thor defeated him by luring him with beef heads, and hitting him with Mjollnir!", bc_map, 0xFF0000;
				sleep2 500;
				instance_announce $jl_instance, "[Urd] I'll summon this memory here, on the down right corner! Lure it to him!", bc_map, 0xFF0000;			
				sleep2 500;
				enablenpc instance_npcname( "alajlt", $jl_instance );
				set .weakenedCount, 0;
			}
			else
			{
				//after 1 minute delay, kill monsters to heal the Snake
				set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
				killmonster .@map$, .@npc_name$+"::OnNormalDied";
				
				instance_announce $jl_instance, "[Skuld] He just eat all those monsters? Why!?", bc_map, 0x00FF00;
				sleep2 500;
				
				//RATHENA
				getunitdata($jormungandId, .@jormData);
				set .@mhp, .@jormData[UMOB_HP];
				set .@mhp, .@jormData[UMOB_MAXHP];
				//HERCULES
				//set .@hp, getunitdata($jormungandId, UDT_HP);
				//set .@mhp, getunitdata($jormungandId, UDT_MAXHP);
				
				if(.@hp < .@mhp * 30 / 100)
				{
					//RATHENA
					setunitdata $jormungandId, 2, .@hp + .@mob_dead_num * (.@mhp * 5 / 100);
					//HERCULES
					//setunitdata($jormungandId, UDT_HP, .@hp + .@mob_dead_num * (.@mhp * 5 / 100));
				}
				else
				{
					//RATHENA
					setunitdata $jormungandId, 2, .@hp + .@mob_dead_num * (.@mhp * 1 / 100);
					//HERCULES
					//setunitdata($jormungandId, UDT_HP, .@hp + .@mob_dead_num * (.@mhp * 1 / 100));
				}
				instance_announce $jl_instance, "[Jormungand] Hisss! I'll devour you too!!", bc_map, 0xFF0000;
				sleep2 500;
				
				instance_announce $jl_instance, "[Verdandi] He's regenerating by eating those!?", bc_map, 0x00FF00;
				sleep2 500;
			}
		}
		if(.weakenedCount == 30)
		{
			summon_normal( .@map$, .@npc_name$, 0 );
		}
	}
	else if($instance_jl_level == 4)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
		
		//RATHENA
		getunitdata($jormungandId, .@jormData);
		set .@mhp, .@jormData[UMOB_HP];
		set .@mhp, .@jormData[UMOB_MAXHP];
		//HERCULES
		//set .@hp, getunitdata($jormungandId, UDT_HP);
		//set .@mhp, getunitdata($jormungandId, UDT_MAXHP);
		if(.@hp <= 50 * .@mhp / 100)
		{
			instance_announce $jl_instance, "[Jormungand] Enough of your play! You want to fight me? Let'sssss do it then, humanssssss!?", bc_map, 0xFF0000;
			sleep2 500;			
			
			$instance_jl_level++;
			killmonster .@map$, .@npc_name$ + "::OnGuardianDied";
			summon_guardian( .@map$, .@npc_name$ );
		}
	}
	setnpctimer 0;
	end;
	
OnFirstFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	//pop monsters
	summon_normal( .@map$,.@npc_name$, 1 );
	instance_announce $jl_instance, "[Jormungand Lair] The first floor of the Lair is filled with monsters.", bc_map, 0xFF0000;
	sleep2 500;
	instance_announce $jl_instance, "[Jormungand Lair] Let's first find how Jormungand can shrink to this little size.", bc_map, 0xFF0000;	
	set $jlContinuousPop, 1;
	set $random2FDiff, rand(0,1);
	end;
	
OnSecondFloor1On:
	set .weakenedPop, 1;
	set $jlUnweakenedPop, 1;
OnSecondFloor2On:
	set $jlContinuousPop, 0;
	
	set .@map$, strnpcinfo(4);
	killmonster .@map$, "All";
	
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	instance_announce $jl_instance, "[Urd] Really? It drops its ribs and scales there, and then he shrinks?", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Skuld] Apparently...", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Urd] But how does he grows back?", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Skuld] I suppose he devour the monsters that drops the Ribs and Scales to grow again to his full size...", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Urd] Yuck...That's gross!", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Verdandi] Enough! Adventurers, you know have to find a way to destroy it. Even reduced, it's fairly dangerous. You canno't defeat it like if it was a simple snake.", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Verdandi] There was an old tale about Beefs and a Hammer. Thor might have make something to him one day...But I can't remember...", bc_map, 0x00FF00;
	sleep 1500;
	instance_announce $jl_instance, "[Verdandi] Oh gods! Be carefull! He's awaking!", bc_map, 0x00FF00;
	
	summon_normal( .@map$, .@npc_name$, 1 );
	summon_guardian( .@map$, .@npc_name$ );
	end;
	
OnThirdFloorCheck:
	set .@ok, 1;
	for(set .@i,0; .@i<10; set .@i, .@i+1)
	{
		if(getd("$jlruneInserted" + .@i) == 0)
		{
			set .@ok, 0;			
		}
	}
	if(.@ok == 1)
	{
		instance_announce $jl_instance, "[Verdandi] Come here, to the deepest point. He's here!", bc_map, 0x00FF00;
		sleep2 5000;
		enablenpc instance_npcname( "alajlj", $jl_instance );
		enablenpc instance_npcname( "alajlve3", $jl_instance );
		enablenpc instance_npcname( "alajlur3", $jl_instance );
		enablenpc instance_npcname( "alajlsk3", $jl_instance );
	}
	end;
	
OnThirdFloorOn:
	$instance_jl_level++;
	set .@map$, strnpcinfo(4);
	set $jorKilled, 0;
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	summon_guardian( .@map$, .@npc_name$ );
	end;
	
OnThirdFloorMobPop:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0), $jl_instance );
	summon_normal( .@map$, .@npc_name$, 0 );
	end;
}

1@jl,253,276,3	script	Bone Pile::alajlbp	4_SOIL,{
	mes "[Skull]";
	mes "Under the dust, you've find something strange.";
	mes "There's a strange pile of bones here...";
	if(countitem($jlRibId) > 0)
	{
		mes "Do you want to dump your Ribs here?";
		if(select("Yes","No") == 1)
		{
			set .countRIB, .countRIB + countitem($jlRibId);
			delitem $jlRibId, countitem($jlRibId);
			
			if(.countRIB >= .neededRibs)
			{
				disablenpc strnpcinfo(3);
				disablenpc instance_npcname( "alajlsp", $jl_instance );
				if($random2FDiff == 0)
				{
					donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnSecondFloor1On";
				}
				else
				{
					donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnSecondFloor2On";
				}
			}
			else
			{
				next;
				mes "[Skull]";
				mes "Bring " + (.neededRibs - .countRIB) + " more.";
			}
		}
	}
	close;
	
OnInstanceInit:
OnEnable:
	set .neededRibs, rand(10);//rand(0,250);
	set .countRIB, 0;
	enablenpc strnpcinfo(3);
	end;
	
OnDisable:
	disablenpc strnpcinfo(3);
	end;
}

1@jl,251,266,3	script	Scale Pile::alajlsp	4_SOIL,{
	mes "[Skin]";
	mes "Under the dust, you've find something strange.";
	mes "There's a strange pile of scales here...";
	if(countitem($jlScaleId) > 0)
	{
		mes "Do you want to dump your Scales here?";
		if(select("Yes","No") == 1)
		{
			set .countScale, .countScale + countitem($jlScaleId);
			delitem $jlScaleId, countitem($jlScaleId);
			
			if(.countScale >= .neededScales)
			{
				disablenpc strnpcinfo(3);
				disablenpc instance_npcname( "alajlbp", $jl_instance );
				if($random2FDiff == 1)
				{
					donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnSecondFloor1On";
				}
				else
				{
					donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnSecondFloor2On";
				}
			}
			else
			{
				next;
				mes "[Skin]";
				mes "Bring " + (.neededScales - .countScale) + " more.";
			}
		}
	}
	close;
	
OnInstanceInit:
OnEnable:
	set .neededScales, rand(10);//rand(0,250);
	set .countScale, 0;
	enablenpc strnpcinfo(3);
	end;
	
OnDisable:
	disablenpc strnpcinfo(3);
	end;
}

1@jl,289,196,3	script	Thor::alajlt	4_M_RUPERT,4,4,{
	end;
	
OnTouch:	
	if(.talking == 0 && $jorKilled == 1)
	{
		set .talking, 1;
		npctalk "That was an epic fight, mortal!";
		instance_announce $jl_instance, "[Thor] That was an epic fight, mortal!", bc_map, 0x0000FF;
		sleep2 500;
		npctalk "You have my friendship. I'm hunting this monster for years now.";
		instance_announce $jl_instance, "[Thor] You have my friendship. I'm hunting this monster for years now.", bc_map, 0x0000FF;
		sleep2 500;
		npctalk "My father want them dead before the Ragnarok comes.";
		instance_announce $jl_instance, "[Thor] My father want them dead before the Ragnarok comes.", bc_map, 0x0000FF;
		sleep2 500;
		instance_announce $jl_instance, "[" + strcharinfo(0) + "] Can you lend us your Hammer?", bc_map, 0x0000FF;
		sleep2 500;
		instance_announce $jl_instance, "[" + strcharinfo(0) + "] This was just a weak version of Jormungand...", bc_map, 0x0000FF;
		sleep2 500;
		npctalk "Yeah I know. But I can't. You're too weak for Mjolnir, it'll kill you.";
		instance_announce $jl_instance, "[Thor] Yeah I know. But I can't. You're too weak for Mjolnir, it'll kill you.", bc_map, 0x0000FF;
		sleep2 500;
		npctalk "But believe me, you're strong enough to fight him. He can't take his true form here.";
		instance_announce $jl_instance, "[Thor] But believe me, you're strong enough to fight him. He can't take his true form here.", bc_map, 0x0000FF;
		sleep2 500;
		npctalk "That's a chance for you. Go on, brave warriors. We'll meet again in Valhalla!";
		instance_announce $jl_instance, "[Thor] That's a chance for you. Go on, brave warriors. We'll meet again in Valhalla!", bc_map, 0x0000FF;
		sleep2 500;
		npctalk "As far as I know, you have great chances to find him at the bottom of the illuminated path.";
		instance_announce $jl_instance, "[Thor] As far as I know, you have great chances to find him at the bottom of the illuminated path.", bc_map, 0x0000FF;
		
		disablenpc strnpcinfo(3);
	}
	end;
	
OnTouchNPC:
	attachrid($jlTalkingId);
	npctalk "Come here, you filthy snake! I'll present you my hammer...";
	instance_announce $jl_instance, "[Thor] Come here, you filthy snake! I'll present you my hammer", bc_map, 0x0000FF;
	sleep2 500;
	npctalk "Here it comes! Mjolnir!";
	instance_announce $jl_instance, "[Thor] Here it comes! Mjolnir!!!", bc_map, 0x0000FF;
	sleep2 500;
	specialeffect 102;
	sleep2 500;
	specialeffect 30;
	npctalk "Ahahaha! I'll kill you!";
	instance_announce $jl_instance, "[Thor] Ahahaha! I'll kill you!", bc_map, 0x0000FF;
	//RATHENA
	getunitdata($jormungandId, .@jormData);
	set .@mhp, .@jormData[UMOB_HP];
	set .@mhp, .@jormData[UMOB_MAXHP];
	//HERCULES
	//set .@hp, getunitdata($jormungandId, UDT_HP);
	//set .@mhp, getunitdata($jormungandId, UDT_MAXHP);
	
	//RATHENA
	setunitdata($jormungandId, 2, .@hp - .@mhp * 10 / 100);
	//HERCULES
	setunitdata($jormungandId, UDT_HP, .@hp - .@mhp * 10 / 100);
	//NPC_INVINCIBLEOFF
	unitskilluseid($jormungandId, 686, 1);
	set $jlUnweakenedPop, -1;
	instance_announce $jl_instance, "[Verdandi] He's vulnerable now! Kill it!", bc_map, 0x00FF00;
	goto OnDisable;
	end;
	
OnInstanceInit:
OnDisable:
	set .talking, 0;
	disablenpc strnpcinfo(3);
	end;
OnEnable:
	enablenpc strnpcinfo(3);
	end;
}

1@jl,190,134,3	script	Rune Socket#0::alajlrs	4_CRACK,4,4,{
	mes "[Rune Socket]";
	mes "There's a small hole here, on the floor...Something might be encarved in it.";
	if(countitem($jlRuneId) > 0)
	{
		mes "You can see a small button. Do you want to encarve a " + getitemname($jlRuneId) + "?";
		if(select("Yes","No") == 1)
		{
			next;
			mes "[Rune Socket]";
			delitem $jlRuneId, 1;
			mes "*Click*";
			mes "Looks like the Mana is running to the wall through the Rune.";
			setd("$jlruneInserted" + strnpcinfo(2), 1);
			
			donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnThirdFloorCheck";
			disablenpc strnpcinfo(3);
		}
	}	
	else
	{
		mes "You can see a small button. Do you want to click it with your finger?";
		if(select("Yes","No") == 1)
		{
			set .@rand, rand(10,50) / 10;
			percentheal .@rand * -10, .@rand * 10;
			getmapxy($jlMap$, $jlx, $jly, UNITTYPE_NPC);
			donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnThirdFloorMobPop";
		}
	}
	close;
	
OnTouch:	
	dispbottom "You can feel a tiny air breeze coming from somewhere around here.";
	end;
	
OnInstanceInit:
OnDisable:
	disablenpc strnpcinfo(3);
	setd("$jlruneInserted" + strnpcinfo(2), 0);
	end;
	
OnEnable:
	enablenpc strnpcinfo(3);
	end;
}

1@jl,206,106,3	duplicate(alajlrs)	Rune Socket#1::alajlrs1	4_CRACK,4,4
1@jl,265,138,3	duplicate(alajlrs)	Rune Socket#2::alajlrs2	4_CRACK,4,4
1@jl,318,145,3	duplicate(alajlrs)	Rune Socket#3::alajlrs3	4_CRACK,4,4
1@jl,349,129,3	duplicate(alajlrs)	Rune Socket#4::alajlrs4	4_CRACK,4,4
1@jl,357,63,3	duplicate(alajlrs)	Rune Socket#5::alajlrs5	4_CRACK,4,4
1@jl,327,46,3	duplicate(alajlrs)	Rune Socket#6::alajlrs6	4_CRACK,4,4
1@jl,312,47,3	duplicate(alajlrs)	Rune Socket#7::alajlrs7	4_CRACK,4,4
1@jl,284,48,3	duplicate(alajlrs)	Rune Socket#8::alajlrs8	4_CRACK,4,4
1@jl,260,58,3	duplicate(alajlrs)	Rune Socket#9::alajlrs9	4_CRACK,4,4


1@jl,187,56,3	script	Jormungand::alajlj	3327,4,4,{
	end;
	
OnTouch:	
	if(.talking == 0)
	{
		set $jlTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "",2;
		npctalk "You're not the welcomed here...";
		instance_announce $jl_instance, "[Jormungand] You're not the welcomed here...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "Why are you investing my Lair?";
		instance_announce $jl_instance, "[Jormungand] Why are you investing my Lair?", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "You've collected my bones and scales, killed my shadow. What do you want more?";
		instance_announce $jl_instance, "[Jormungand] You've collected my bones and scales, killed my shadow. What do you want more?", bc_map, 0x00FF00;
		sleep2 500;
			
		donpcevent instance_npcname( "alajlve3", $jl_instance ) + "::OnDialog3_2";
	}
	end;
	
OnDialog3_3:
	attachrid($jlTalkingId);
	npctalk "Try to defeat me before sssssssaying that, humansssssss...";
	instance_announce $jl_instance, "[Jormungand] Try to defeat me before sssssssaying that, humansssssss...", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "I'll feassssst over your bonesssss...";
	instance_announce $jl_instance, "[Jormungand] I'll feassssst over your bonesssss", bc_map, 0x00FF00;
	
	disablenpc strnpcinfo(3);
	disablenpc instance_npcname( "alajlve3", $jl_instance );
	disablenpc instance_npcname( "alajlur3", $jl_instance );
	disablenpc instance_npcname( "alajlsk3", $jl_instance );
	
	cutin "", 255;
	
	donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnThirdFloorOn";	
	end;
	
OnInstanceInit:	
OnDisable:
	disablenpc strnpcinfo(3);
	set .talking, 0;
	end;
OnEnable:
	enablenpc strnpcinfo(3);
	end;
}

1@jl,242,276,3	script	Verdandi::alajlve	4_F_NYDHOG,2,2,{
	//cutin "ep14_nyd01",2;
	end;
		
OnTouch:	
	if(.talking == 0)
	{
		set $jlTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "ep14_nyd01",2;
		npctalk "Humpf. This place smell like death...";
		instance_announce $jl_instance, "[Verdandi] Humpf. This place smell like death...", bc_map, 0x00FF00;
		sleep2 500;
	
		donpcevent instance_npcname( "alajlur", $jl_instance ) + "::OnDialog1_2";
	}
	end;
	
OnDialog1_4:
	attachrid($jlTalkingId);
	cutin "ep14_nyd01", 2;
	npctalk "Adventurer, we're here on Jormungand Lair. This place is where the World Snake goes when he reduces his size.";
	instance_announce $jl_instance, "[Verdandi] Adventurer, we're here on Jormungand Lair. This place is where the World Snake goes when he reduces his size.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "He's able to shrink to enter in this place. But that doesn't mean it's less dangerous.";
	instance_announce $jl_instance, "[Verdandi] He's able to shrink to enter in this place. But that doesn't mean it's less dangerous.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "You'll have to find a way to reach his very own lair. We don't know how it works. Hope you'll find your way.";
	instance_announce $jl_instance, "[Verdandi] You'll have to find a way to reach his very own lair. We don't know how it works. Hope you'll find your way.", bc_map, 0x00FF00;
	sleep2 500;
	
	cutin "", 255;
	
	disablenpc strnpcinfo(3);
	disablenpc instance_npcname( "alajlur", $jl_instance );
	disablenpc instance_npcname( "alajlsk", $jl_instance );
	
	donpcevent instance_npcname( "jormungand_lair", $jl_instance ) + "::OnFirstFloorOn";
	end;	

OnInstanceInit:
	set .talking, 0;
	enablenpc strnpcinfo(3);
	end;
}

1@jl,243,273,5	script	Urd::alajlur	4_F_PINKWOMAN,{
	//cutin "bu_maggi1",0;
	end;
		
OnDialog1_2:			
	attachrid($jlTalkingId);
	cutin "bu_maggi1", 2;
	npctalk "Sister, please, I don't want to stay there...";
	instance_announce $jl_instance, "[Urd] Sister, please, I don't want to stay there...", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alajlsk", $jl_instance ) + "::OnDialog1_3";
	end;
	
OnInstanceInit:
	enablenpc strnpcinfo(3);
	end;
}

1@jl,246,274,5	script	Skuld::alajlsk	4_F_SHALOSH,{
	//cutin "shaloshi01",2;
	end;
		
OnDialog1_3:			
	attachrid($jlTalkingId);
	cutin "shaloshi01", 2;
	npctalk "Silence, Urd. Do you think we're here cause we wanted it?";
	instance_announce $jl_instance, "[Skuld] Silence, Urd. Do you think we're here cause we wanted it?", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alajlve", $jl_instance ) + "::OnDialog1_4";
	end;
	
OnInstanceInit:
	enablenpc strnpcinfo(3);
	end;
}

1@jl,292,191,3	script	Verdandi::alajlve2	4_F_NYDHOG,2,2,{
	//cutin "ep14_nyd01",2;
	end;
		
OnTouch:	
	if(.talking == 0)
	{
		set $jlTalkingId, getcharid(3);
		set .talking, 1;	

		cutin "ep14_nyd01",2;
		npctalk "You've defeated this filthy beast...";
		instance_announce $jl_instance, "[Verdandi] You've defeated this filthy beast...", bc_map, 0x00FF00;
		sleep2 500;
	
		donpcevent instance_npcname( "alajlur2", $jl_instance ) + "::OnDialog2_2";
	}
	end;
	
OnDialog2_4:
	attachrid($jlTalkingId);
	cutin "ep14_nyd01", 2;
	npctalk "Don't worry. It'll shows up in time. I can feel it in the Present.";
	instance_announce $jl_instance, "[Verdandi] Don't worry. It'll shows up in time. I can feel it in the Present.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "I suppose he's waiting for something, even though I don't know what exactly.";
	instance_announce $jl_instance, "[Verdandi] I suppose he's waiting for something, even though I don't know what exactly.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "It's up to you. You're used to that kind of thing, isn't it?";
	instance_announce $jl_instance, "[Verdandi] It's up to you. You're used to that kind of thing, isn't it?", bc_map, 0x00FF00;
	sleep2 500;
	
	cutin "", 255;
	
	$instance_jl_level++;
	
	disablenpc instance_npcname( "alajlt", $jl_instance );
	disablenpc instance_npcname( "alajlur2", $jl_instance );
	disablenpc instance_npcname( "alajlsk2", $jl_instance );
	
	enablenpc instance_npcname( "alajlrs", $jl_instance );
	enablenpc instance_npcname( "alajlrs1", $jl_instance );
	enablenpc instance_npcname( "alajlrs2", $jl_instance );
	enablenpc instance_npcname( "alajlrs3", $jl_instance );
	enablenpc instance_npcname( "alajlrs4", $jl_instance );
	enablenpc instance_npcname( "alajlrs5", $jl_instance );
	enablenpc instance_npcname( "alajlrs6", $jl_instance );
	enablenpc instance_npcname( "alajlrs7", $jl_instance );
	enablenpc instance_npcname( "alajlrs8", $jl_instance );
	enablenpc instance_npcname( "alajlrs9", $jl_instance );
	
	disablenpc strnpcinfo(3);
	end;	

OnInstanceInit:
	set .talking, 0;
	disablenpc strnpcinfo(3);
	end;
}

1@jl,298,187,5	script	Urd::alajlur2	4_F_PINKWOMAN,{
	//cutin "bu_maggi1",0;
	end;
		
OnDialog2_2:			
	attachrid($jlTalkingId);
	cutin "bu_maggi1", 2;
	npctalk "Yes, but I can't see the real one anywhere. A big snake like this can't really hide anywhere here...";
	instance_announce $jl_instance, "[Urd] Yes, but I can't see the real one anywhere. A big snake like this can't really hide anywhere here...", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alajlsk2", $jl_instance ) + "::OnDialog2_3";
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

1@jl,295,185,5	script	Skuld::alajlsk2	4_F_SHALOSH,{
	//cutin "shaloshi01",2;
	end;
		
OnDialog2_3:			
	attachrid($jlTalkingId);
	cutin "shaloshi01", 2;
	npctalk "Where is it...How is this possible?";
	instance_announce $jl_instance, "[Skuld] Where is it...How is this possible?", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "Thor did tell us it would be there, right?";
	instance_announce $jl_instance, "[Skuld] Thor did tell us it would be there, right?", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alajlve2", $jl_instance ) + "::OnDialog2_4";
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

1@jl,186,51,0	script	Verdandi::alajlve3	4_F_NYDHOG,2,2,{
	//cutin "ep14_nyd01",2;
	end;
	
OnTouch:
	if($instance_jl_level == 5 && $jorKilled == 1 && .talking == 0)
	{
		set $jlTalkingId, getcharid(3);
		set .talking, 1;	
		
		cutin "ep14_nyd01", 2;
		npctalk "It was a very tough battle...";
		instance_announce $jl_instance, "[Verdandi] It was a very tough battle...", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "But thanks to you, we've defeated the Snake.";
		instance_announce $jl_instance, "[Verdandi] But thanks to you, we've defeated the Snake.", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "We're walking to the Ragnarok failure!";
		instance_announce $jl_instance, "[Verdandi] We're walking to the Ragnarok failure!", bc_map, 0x00FF00;
		sleep2 500;
		npctalk "Soon...Very soon!";
		instance_announce $jl_instance, "[Verdandi] Soon...Very soon!", bc_map, 0x00FF00;
		sleep2 5000;
		instance_destroy( $jl_instance );
		set $jl_instance, -1;
		set $instance_jl_id, -1;
	}
	end;
	
OnDialog3_2:
	attachrid($jlTalkingId);
	cutin "ep14_nyd01", 2;
	npctalk "We're here to prevent the Ragnarok.";
	instance_announce $jl_instance, "[Verdandi] We're here to prevent the Ragnarok.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "We don't need any kind of authorization.";
	instance_announce $jl_instance, "[Verdandi] We don't need any kind of authorization.", bc_map, 0x00FF00;
	sleep2 500;
	npctalk "But don't worry, you won't be the only one to be defeated. All major Ases and Giants will be.";
	instance_announce $jl_instance, "[Verdandi] But don't worry, you won't be the only one to be defeated. All major Ases and Giants will be.", bc_map, 0x00FF00;
	sleep2 500;
	
	donpcevent instance_npcname( "alajlj", $jl_instance ) + "::OnDialog3_3";
	end;	

OnInstanceInit:
	set .talking, 0;
	disablenpc strnpcinfo(3);
	end;
}

1@jl,190,50,2	script	Urd::alajlur3	4_F_PINKWOMAN,{
	//cutin "bu_maggi1",0;
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

1@jl,192,54,3	script	Skuld::alajlsk3	4_F_SHALOSH,{
	//cutin "shaloshi01",2;
	end;
	
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

