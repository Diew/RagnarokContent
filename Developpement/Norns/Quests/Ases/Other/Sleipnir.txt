// [========================================================]
// [===========  	Main Line - Sleipnir	============]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - Rune Midgard main story quest line    	   	    ]
// [  This file contains explanation and base npcs 	    ]
// [                                                        ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [========================================================]
// [                                                        ]
// [========================================================]

um_fild01,245,155,4	script	Sleipnir::Ase_1_3_1	4_M_IAN,{ //10156
	set .@npcname$, "[Sleipnir]";
	set .@pcname$, "[" + strcharinfo(0) + "]";
	
	mes .@npcname$;
	//cutin Sleipnir
	mes "Who are you, mortal?";
	mes "I'm Sleipnir, Odin's horse, changed into human for the incoming war.";
	query_sql("SELECT `sealId`,`generalState` FROM `verdandi_quests` WHERE `id`='1_3'", .@sealItem, .@generalState);
	if(countitem(.@sealItem[0]) > 0 && .@generalState[0] == 2)
	{
		switch(rand(0,10))
		{
			case 0:
			case 1:
			case 2:
				//nothing, stop talking from this point
				break;
			case 3:
				next;
				mes .@npcname$;
				mes "You look a bit strange...Are you hidding something from me?";
				break;
			case 4:
				next;
				mes .@npcname$;
				mes "When somebody tells you his name, it's polite to tell him yours, isn't it?";
				break;
			case 5:
				next;
				mes .@npcname$;
				mes "...Are you sure I've never seen you before?";
				break;
			case 6:
				next;
				mes .@npcname$;
				mes "There's a strange smell over you...What is it?";
				break;
			case 7:
				next;
				mes .@npcname$;
				mes "There's a strange aura issuing from you...What is it?";
				break;
			case 8:
				next;
				mes .@npcname$;
				mes "Go away. I'm a bit busy now.";
				break;
			case 9:
				next;
				mes .@npcname$;
				mes "That's kinda strange you've been able to see me...Who are you?";
				break;
			case 10:
				next;
				cutin "", 255;
				mes .@pcname$;
				mes "What do you think about the War?";
				next;
				//cutin sleipnir
				mes .@npcname$;
				mes "I've served him for decades as a mount, and now, I'm about to fight alongside with him.";
				mes "Of course I'm impatient to fight!";
				next;
				cutin "", 255;
				mes .@pcname$;
				mes "Thanks...You remove my last hesitations.";
				delitem .@sealItem[0], 1;
				goto OnSealActivated;
				break;
		}
	}
	close;
	
OnSealActivated:
	set .@skuldName$, "[Skuld]";
	cutin "shaloshi01",2;
	next;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found he's weakness.";
	mes "To seal him, you'll have to kill the right mob in the summoned ones.";
	mes "Once done, come back to Verdandi. She'll be able to Seal him completely.";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'm on it.";
	close;
	
OnActivateTarget:
	set .@activatedRank, rand(1,4);
	cutin "shaloshi01",2;
	set .@skuldName$, "[Skuld]";	
	explode(.@id$, strnpcinfo(3), "_");
	set .@realid$, .@id$[1] + "_"  + .@id$[2];
	set .@npcname$, .@id$[0] + "_"  + .@realid$ + "_" + .@activatedRank;
	hideoffnpc .@npcname$;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found him.";
	mes "He's on Umbala Field 0" + .@activatedRank + ".";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'll get him.";
	set .@query$, "UPDATE `verdandi_quests` SET `ActivatedNpc`=" + .@activatedRank + " WHERE `id`='" + .@realid$ + "'";
	query_sql(.@query$, .@count);
	close;
	
OnSealTarget:
	hideonnpc strnpcinfo(3);
	hideonnpc "Ase_1_3_2";
	hideonnpc "Ase_1_3_3";
	hideonnpc "Ase_1_3_4";
	query_sql("UPDATE `verdandi_quests` SET `generalState`=3 WHERE `id`='1_3'", .@count);
	announce " [Verdandi] The Ase Sleipnir has been Sealed successfully thanks to " + strcharinfo(0) + " and his team.",bc_all,"0x00FF99";
	getpartymember getcharid(2),2;
	for( set .@pt, 0; .@pt < $@partymembercount; set .@pt, .@pt+1 ){
		if( attachrid( $@partymemberaid[.@pt] ) ){
			callfunc("AddPoints", getcharid(3), $sealingPointAmount);
			logmes "" + strcharinfo(0) + " obtains " + ($sealingPointAmount) + " points from sealing Sleipnir.";
		}
	}
	close;
	
		
OnMyMobKilled:
	query_sql("SELECT `generalState` FROM `verdandi_quests` WHERE `id`='1_3'", .@generalState);
	if(.@generalState[0] != 3)
	{
		monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[rand(0,getarraysize(.monsterIds) - 1)],1,"Ase_1_3_1::OnMyMobKilled";
	}
	end;

OnRightMonsterKilled:
	query_sql("SELECT `generalState` FROM `verdandi_quests` WHERE `id`='1_3'", .@generalState);
	if(.@generalState[0] == 2)
	{
		set .@skuname$, "[Skuld]";
		cutin "shaloshi01",2;
		mes .@skuname$;
		mes "I got him! I found his Future Thread. Take it to Verdandi now!";
		getitem $sleipnirSealItemId, 1;
		next;
		cutin "", 255;
		mes "[" + strcharinfo(0) + "]";
		mes "On my way!";
	}
	else
	{
		monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[getarraysize(.monsterIds) - 1],.@invokCount,"Ase_1_3_1::OnRightMonsterKilled";
	}
	close;
	
OnCallMonsters:
	query_sql("SELECT `generalState` FROM `verdandi_quests` WHERE `id`='1_3'", .@generalState);
	if(.@generalState[0] != 3)
	{
		set .@size, getarraysize(.monsterIds);
		for(set .@i,0; .@i<.@size && .monsterInvoked < .monsterInvokLimit; set .@i,.@i+1)
		{
			set .@invokCount, rand(1,10);
			monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[rand(0,getarraysize(.monsterIds) - 1)],.@invokCount,"Ase_1_3_1::OnMyMobKilled";
			set .monsterInvoked, .monsterInvoked + .@invokCount;
		}
		monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[getarraysize(.monsterIds) - 1],.@invokCount,"Ase_1_3_1::OnRightMonsterKilled";
	}
	end;
	
OnTimer10000:
	if (strnpcinfo(3) == "Ase_1_3_1")
	{
		query_sql("SELECT `ActivatedNpc`,`generalState` FROM `verdandi_quests` WHERE `id`='1_3'", .@npc$, .@generalState);
		if(.@npc$[0] != "0" && .@generalState[0] == 2)
		{
			hideoffnpc "Ase_1_3_" + .@npc$[0];
		}
		stopnpctimer;
	}
	end;
	
OnInit:
	hideonnpc strnpcinfo(3);
	initnpctimer;
	set .monsterInvokLimit, 99;
	set .monsterInvoked, 0;
	setarray .monsterIds[0], 1061, 2071, 1250;
	setarray .influencedMaps$[0], "um_fild01" "um_fild02", "um_fild03", "um_fild04", "um_dun01", "um_dun02";	
	set $sleipnirSealItemId, 27006;
	if(strnpcinfo(3) == "Ase_1_3_1")
	{
		initnpctimer;
		goto OnCallMonsters;
	}
	end;
}

um_fild02,151,133,4	duplicate(Ase_1_3_1)	Sleipnir::Ase_1_3_2	4_M_IAN
um_fild03,219,210,4	duplicate(Ase_1_3_1)	Sleipnir::Ase_1_3_3	4_M_IAN
um_fild04,170,58,4	duplicate(Ase_1_3_1)	Sleipnir::Ase_1_3_4	4_M_IAN