// [========================================================]
// [===========  	 Main Line - Freyja	============]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - Rune Midgard main story quest line    	   	    ]
// [  This file contains explanation and base npcs 	    ]
// [                                                        ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [========================================================]
// [                                                        ]
// [========================================================]

cmd_fild01,97,225,4	script	Freyja::Ase_2_3_1	4_SERVICE_30_F_01,{ //10146
	set .@npcname$, "[Freyja]";
	set .@pcname$, "[" + strcharinfo(0) + "]";
	
	mes .@npcname$;
	//cutin Freyja
	mes "Who are you, mortal?";
	mes "I'm Freyja, Father of Everything.";
	mes "I'll lead my Ases, my Vanes and all the Braves to the last fight.";
	mes "Not that I like it, even though I don't dislike to prove my valor. But your race have the rights to live free.";
	query_sql("SELECT `sealId`,`generalState` FROM `verdandi_quests` WHERE `id`='2_3'", .@sealItem, .@generalState);
	if(FreyjaQ == 0)
	{
		if(countitem(.@sealItem[0]) > 0 && .@generalState[0] == 2)
		{
			switch(rand(0,10))
			{
				case 0:
				case 1:
				case 2:
					//nothing, stop talking from this point
					break;
				case 3:
					next;
					mes .@npcname$;
					mes "You look a bit strange...Are you hidding something from me?";
					break;
				case 4:
					next;
					mes .@npcname$;
					mes "When somebody tells you his name, it's polite to tell him yours, isn't it?";
					break;
				case 5:
					next;
					mes .@npcname$;
					mes "...Are you sure I've never seen you before?";
					break;
				case 6:
					next;
					mes .@npcname$;
					mes "There's a strange smell over you...What is it?";
					break;
				case 7:
					next;
					mes .@npcname$;
					mes "There's a strange aura issuing from you...What is it?";
					break;
				case 8:
					next;
					mes .@npcname$;
					mes "Go away. I'm a bit busy now.";
					break;
				case 9:
					next;
					mes .@npcname$;
					mes "That's kinda strange you've been able to see me...Who are you?";
					break;
				case 10:
					next;
					cutin "", 255;
					mes .@pcname$;
					mes "What do you think about the War?";
					next;
					//cutin Hugin
					mes .@npcname$;
					mes "The Ragnarok? As I said, it's not that I like it. It's that it has to come.";
					mes "Soon...Soon the brave, the Valkyries, the Einenjar...Everything will end.";
					next;
					cutin "", 255;
					mes .@pcname$;
					mes "Thanks...You remove my last hesitations.";
					delitem .@sealItem[0], 1;
					goto OnSealActivated;
					break;
			}
		}
	}
	else
	{
		next;
		mes .@npcname$;
		mes "So you're here to stop me. I can understand that. But you don't have the choice, believe me.";
		mes "Now, I'll have to ensure that you won't raise against me. Don't mind me, I admire your courage.";
		getmapxy(.@map$, .@x, .@y, UNITTYPE_NPC);
		set .FreyjaPoped, 1;
		hideonnpc strnpcinfo(3);
		monster .@map$, .@x, .@y, "Freyja", x, 1, strnpcinfo(3) + "::OnFreyjaDied";
	}
	close;
	
OnSealActivated:
	set .@skuldName$, "[Skuld]";
	next;
	cutin "shaloshi01",2;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found he's weakness.";
	mes "To seal him, you'll have to find an answer to his question...Simple as that, hu?!";
	mes "Once done, come back to Verdandi. She'll be able to Seal him completely.";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'm on it.";
	set FreyjaQ, 1;
	close;
	
OnFreyjaDied:
	set .@npcname$, "[Freyja]";
	next;
	mes .@npcname$;
	mes "You...You defeated me...";
	next;
	//condition fullfilled
	set .@skuname$, "[Skuld]";
	cutin "shaloshi01",2;
	mes .@skuname$;
	mes "I got him! I found his Future Thread. Take it to Verdandi now!";
	getitem $FreyjaSealItemId, 1;
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "On my way!";
	close;
	
OnSealTarget:
	specialeffect 910; //inspiration
	hideonnpc strnpcinfo(3);
	hideonnpc "Ase_2_3_2";
	hideonnpc "Ase_2_3_3";
	hideonnpc "Ase_2_3_4";
	query_sql("UPDATE `verdandi_quests` SET `generalState`=3 WHERE `id`='2_3'", .@count);
	announce " [Verdandi] The Ase Freyja has been Sealed successfully thanks to " + strcharinfo(0) + " and his team.",bc_all,"0x00FF99";
	getpartymember getcharid(2),2;
	for( set .@pt, 0; .@pt < $@partymembercount; set .@pt, .@pt+1 ){
		if( attachrid( $@partymemberaid[.@pt] ) ){
			callfunc("AddPoints", getcharid(3), $sealingPointAmount);
			logmes "" + strcharinfo(0) + " obtains " + ($sealingPointAmount) + " points from sealing Freyja.";
		}
	}
	close;	
	
OnActivateTarget:
	set .@activatedRank, rand(1,4);
	cutin "shaloshi01",2;
	set .@skuldName$, "[Skuld]";	
	explode(.@id$, strnpcinfo(3), "_");
	set .@realid$, .@id$[1] + "_"  + .@id$[2];
	set .@npcname$, .@id$[0] + "_"  + .@realid$ + "_" + .@activatedRank;
	hideoffnpc .@npcname$;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found him.";
	mes "He's on Comodo Field 0" + .@activatedRank + ".";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'll get him.";
	set .@query$, "UPDATE `verdandi_quests` SET `ActivatedNpc`=" + .@activatedRank + " WHERE `id`='" + .@realid$ + "'";
	query_sql(.@query$, .@count);
	close;

OnTimer10000:
	if(.FreyjaPoped == 0)
	{
		if (strnpcinfo(3) == "Ase_2_3_1")
		{
			query_sql("SELECT `ActivatedNpc`,`generalState` FROM `verdandi_quests` WHERE `id`='2_3'", .@npc$, .@generalState);
			if(.@npc$[0] != "0" && .@npc$[0] != "" && .@generalState[0] == 2)
			{
				hideoffnpc "Ase_2_3_" + .@npc$[0];
			}
		}
	}
	
	unitwarp strnpcinfo(3), strnpcinfo(4), rand(150), rand(150);
	unitskilluseid strnpcinfo(3), MG_THUNDERSTORM, 10; //21
	setnpctimer 0;
	end;
	
OnInit:
	hideonnpc strnpcinfo(3);
	initnpctimer;
	setarray .influencedMaps$[0], "cmd_fild01", "cmd_fild02", "cmd_fild03", "cmd_fild04", "cmd_fild05", "cmd_fild06", "cmd_fild07", "cmd_fild08", "cmd_fild09";
	set $FreyjaSealItemId, 27004;
	set .FreyjaPoped, 0;
	end;
}

cmd_fild02,369,364,4	duplicate(Ase_2_3_1)	Freyja::Ase_2_3_2	4_SERVICE_30_F_01
cmd_fild03,269,240,4	duplicate(Ase_2_3_1)	Freyja::Ase_2_3_3	4_SERVICE_30_F_01
cmd_fild04,276,148,4	duplicate(Ase_2_3_1)	Freyja::Ase_2_3_4	4_SERVICE_30_F_01