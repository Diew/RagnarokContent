// [========================================================]
// [===========  	 Giant - Skoll		============]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - Rune Midgard main story quest line    	   	    ]
// [  This file contains explanation and base npcs 	    ]
// [                                                        ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [========================================================]
// [                                                        ]
// [========================================================]

yuno_fild05,166,217,4	script	Skoll::Giant_3_2_1	4_EP16_STOLZ,{ //10120
	set .@npcname$, "[Skoll]";
	set .@pcname$, "[" + strcharinfo(0) + "]";
	
	mes .@npcname$;
	//cutin Skoll
	mes "*Growl*";
	mes "What do you want, mortal?";
	mes "I'm Sköll, son of Fenrir, the Great Wolf.";
	query_sql("SELECT `sealId`,`generalState` FROM `verdandi_quests` WHERE `id`='3_2'", .@sealItem, .@generalState);
	if(countitem(.@sealItem[0]) > 0 && .@generalState[0] == 2)
	{
		switch(rand(0,10))
		{
			case 0:
			case 1:
			case 2:
				//nothing, stop talking from this point
				break;
			case 3:
				next;
				mes .@npcname$;
				mes "You look a bit strange...Are you hidding something from me?";
				break;
			case 4:
				next;
				mes .@npcname$;
				mes "I can smell the Fear on you. Are you affraid, tiny mortal?";
				break;
			case 5:
				next;
				mes .@npcname$;
				mes "...Are you sure I've never seen you before?";
				break;
			case 6:
				next;
				mes .@npcname$;
				mes "There's a strange smell over you...What is it?";
				break;
			case 7:
				next;
				mes .@npcname$;
				mes "There's a strange aura issuing from you...What is it?";
				break;
			case 8:
				next;
				mes .@npcname$;
				mes "Go away. I'm a bit busy now.";
				break;
			case 9:
				next;
				mes .@npcname$;
				mes "That's kinda strange you've been able to see me...Who are you?";
				break;
			case 10:
				next;
				cutin "", 255;
				mes .@pcname$;
				mes "What do you think about the War?";
				next;
				//cutin Hugin
				mes .@npcname$;
				mes "The Ragnarok? I can't wait to drip my fang inside those filthy bastards flesh and blood.";
				mes "Soon, our hate will be released...";
				next;
				cutin "", 255;
				mes .@pcname$;
				mes "Thanks...You remove my last hesitations.";
				delitem .@sealItem[0], 1;
				goto OnSealActivated;
				break;
		}
	}
	close;
	
OnSealActivated:
	set .@skuldName$, "[Skuld]";
	cutin "shaloshi01",2;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found he's weakness.";
	mes "To seal him, you'll have to kill a " + getmonsterinfo(.monsterIds[getarraysize(.monsterIds) - 1], 0) + ".";
	mes "Once done, come back to me. She'll be able to Seal him completely.";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'm on it.";
	close;
	
OnActivateTarget:
	set .@activatedRank, rand(1,5);
	cutin "shaloshi01",2;
	set .@skuldName$, "[Skuld]";
	explode(.@id$, strnpcinfo(3), "_");
	set .@realid$, .@id$[1] + "_"  + .@id$[2];
	set .@npcname$, .@id$[0] + "_"  + .@realid$ + "_" + .@activatedRank;
	hideoffnpc .@npcname$;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found him.";
	mes "He's on Yuno Field 0" + .@activatedRank + ".";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'll get him.";
	set .@query$, "UPDATE `verdandi_quests` SET `ActivatedNpc`=" + .@activatedRank + " WHERE `id`='" + .@realid$ + "'";
	query_sql(.@query$, .@count);
	close;
	
OnSealTarget:
	hideonnpc strnpcinfo(3);
	hideonnpc "Giant_3_2_2";
	hideonnpc "Giant_3_2_3";
	hideonnpc "Giant_3_2_4";
	query_sql("UPDATE `verdandi_quests` SET `generalState`=3 WHERE `id`='3_2'", .@count);
	announce " [Verdandi] The Giant Skoll has been Sealed successfully thanks to " + strcharinfo(0) + " and his team.",bc_all,"0x00FF99";
	getpartymember getcharid(2),2;
	for( set .@pt, 0; .@pt < $@partymembercount; set .@pt, .@pt+1 ){
		if( attachrid( $@partymemberaid[.@pt] ) ){
			callfunc("AddPoints", getcharid(3), $sealingPointAmount);
			logmes "" + strcharinfo(0) + " obtains " + ($sealingPointAmount) + " points from sealing Skoll.";
		}
	}
	close;
	
OnSealMonsterKilled:	
	query_sql("SELECT `generalState`,`sealId` FROM `verdandi_quests` WHERE `id`='3_2'", .@generalState, .@sealId);
	if(.@generalState[0] != 3)
	{
		if(countitem(.@sealId[0]) == 0)
		{
			monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[getarraysize(.monsterIds) - 1],1,"Giant_3_2_1::OnSealMonsterKilled";
		}
		else
		{
			//condition fullfilled
			set .@skuname$, "[Skuld]";
			cutin "shaloshi01",2;
			mes .@skuname$;
			mes "I got him! I found his Future Thread. Take it to Verdandi now!";
			getitem $skollSealItemId, 1;
			next;
			cutin "", 255;
			mes "[" + strcharinfo(0) + "]";
			mes "On my way!";
			close;
		}
	}
	end;
	
OnMyMobKilled:
	query_sql("SELECT `generalState` FROM `verdandi_quests` WHERE `id`='3_2'", .@generalState);
	if(.@generalState[0] != 3)
	{
		monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[rand(0,getarraysize(.monsterIds) - 2)],1,"Giant_3_2_1::OnMyMobKilled";
	}
	end;
	
OnCallMonsters:
	query_sql("SELECT `generalState` FROM `verdandi_quests` WHERE `id`='3_2'", .@generalState);
	if(.@generalState[0] != 3)
	{
		set .@size, getarraysize(.monsterIds) - 1;
		for(set .@i,0; .@i<.@size && .monsterInvoked < .monsterInvokLimit; set .@i,.@i+1)
		{
			set .@invokCount, rand(1,10);
			monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[rand(0,getarraysize(.monsterIds) - 2)],.@invokCount,"Giant_3_2_1::OnMyMobKilled";
			set .monsterInvoked, .monsterInvoked + .@invokCount;
		}
		monster .influencedMaps$[rand(0,getarraysize(.influencedMaps$) - 1)],0,0,"--ja--",.monsterIds[getarraysize(.monsterIds) - 1],1,"Giant_3_2_1::OnSealMonsterKilled";
	}
	end;

OnTimer10000:
	if (strnpcinfo(3) == "Giant_3_2_1")
	{
		query_sql("SELECT `ActivatedNpc`,`generalState` FROM `verdandi_quests` WHERE `id`='3_2'", .@npc$, .@generalState);
		if(.@npc$[0] != "0" && .@generalState[0] == 2)
		{
			hideoffnpc "Giant_3_2_" + .@npc$[0];
		}
		stopnpctimer;
	}
	end;
	
OnInit:
	hideonnpc strnpcinfo(3);
	initnpctimer;
	set .monsterInvokLimit, 105;
	set .monsterInvoked, 0;
	set $skollSealItemId, 27010;
	setarray .monsterIds[0], 2072, 1106, 1092, 1013, 1785;
	setarray .influencedMaps$[0], "yuno_fild01", "yuno_fild02", "yuno_fild03", "yuno_fild04", "yuno_fild05", "yuno_fild06", "yuno_fild07", "yuno_fild08", "yuno_fild09", "yuno_fild10", "yuno_fild11", "yuno_fild12";
	if(strnpcinfo(3) == "Giant_3_2_1")
	{
		initnpctimer;
		goto OnCallMonsters;
	}
	end;
}

yuno_fild06,308,341,4	duplicate(Giant_3_2_1)	Skoll::Giant_3_2_2	4_EP16_STOLZ
yuno_fild07,227,325,4	duplicate(Giant_3_2_1)	Skoll::Giant_3_2_3	4_EP16_STOLZ
yuno_fild08,297,169,4	duplicate(Giant_3_2_1)	Skoll::Giant_3_2_4	4_EP16_STOLZ