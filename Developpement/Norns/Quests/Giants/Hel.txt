// [========================================================]
// [===========  	 Giant - Hel		============]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - Rune Midgard main story quest line    	   	    ]
// [  This file contains explanation and base npcs 	    ]
// [                                                        ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [========================================================]
// [                                                        ]
// [========================================================]

cmd_fild05,255,353,4	script	Hel::Giant_2_2_1	4_JP_2015EVT,{ //10168
	set .@npcname$, "[Hel]";
	set .@pcname$, "[" + strcharinfo(0) + "]";
	
	mes .@npcname$;
	//cutin Hel
	mes "What do you want, mortal?";
	mes "My name is Hel. I'm the sister of Fenrir and Jormungandr, daughter of Loki.";
	mes "I'm the Guardian of Hell.";
	if(HELCUREQ == 0)
	{
		query_sql("SELECT `sealId`,`generalState` FROM `verdandi_quests` WHERE `id`='2_2'", .@sealItem, .@generalState);
		if(countitem(.@sealItem[0]) > 0 && .@generalState[0] == 2)
		{
			switch(rand(0,10))
			{
				case 0:
				case 1:
				case 2:
					//nothing, stop talking from this point
					break;
				case 3:
					next;
					mes .@npcname$;
					mes "You look a bit strange...Are you hidding something from me?";
					break;
				case 4:
					next;
					mes .@npcname$;
					mes "I can smell the Fear on you. Are you affraid, tiny mortal?";
					break;
				case 5:
					next;
					mes .@npcname$;
					mes "...Are you sure I've never seen you before?";
					break;
				case 6:
					next;
					mes .@npcname$;
					mes "There's a strange smell over you...What is it?";
					break;
				case 7:
					next;
					mes .@npcname$;
					mes "There's a strange aura issuing from you...What is it?";
					break;
				case 8:
					next;
					mes .@npcname$;
					mes "Go away. I'm a bit busy now.";
					break;
				case 9:
					next;
					mes .@npcname$;
					mes "That's kinda strange you've been able to see me...Who are you?";
					break;
				case 10:
					next;
					cutin "", 255;
					mes .@pcname$;
					mes "What do you think about the War?";
					next;
					//cutin Hugin
					mes .@npcname$;
					mes "The Ragnarok? Humm let me think about it.";
					mes "I was once as beautiful as Freya or Frigg.";
					mes "but Thor, to punish my father, burned my face with his thunder.";
					mes "I think I can't be more impatient to kill this bastard.";
					next;
					cutin "", 255;
					mes .@pcname$;
					mes "Thanks...I guess...And sorry about that.";
					delitem .@sealItem[0], 1;
					goto OnSealActivated;
					break;
			}
		}
	}
	else
	{
		next;
		mes .@npcname$;
		mes "So you're trying to find the cure, hu?";
		mes "Don't think it'll be easy, mortal...";
		goto OnComposeCure;
	}
	close;
	
OnComposeCure:
	set .@npcname$, "[Hel]";
	set .@menu$, "";
	for(set .@i,0; .@i<getarraysize(.itemIds); set .@i,.@i+1)
	{
		set .@menu$, .@menu$ + (.@i == 0 ? "" : ":") + getitemname(.itemIds[.@i]);
	}
	set .@menu2$, "";
	for(set .@i,0; .@i<getarraysize(.itemCount); set .@i,.@i+1)
	{
		set .@menu2$, .@menu2$ + (.@i == 0 ? "" : ":") + .itemCount[.@i];
	}
	set .@correct, 0;
	for(set .@i,0; .@i<.herbCountNeeded; set .@i,.@i+1)
	{
		next;
		mes .@npcname$;
		if(.@correct > 0)
		{
			mes "Humpf, allright...";
			mes "Which herb do you wanna use next?";
		}
		else
		{
			mes "Which herb do you wanna use?";
		}
		set .@choice, select(.@menu$) - 1;
		next;
		mes .@npcname$;
		mes "How many of these do you wanna use?";
		set .@count, select(.@menu2$) - 1;
		next;
		mes .@npcname$;
		mes "So you've chosen " + .itemCount[.@count] + " " + getitemname(.itemIds[.@choice]);
		if(countitem(.itemIds[.@choice]) < .itemCount[.@count])
		{
			next;
			mes .@npcname$;
			mes "You don't have those items.";
			mes "Don't try to fool me, mortal.";
			if (countitem(.itemIds[.@choice]) > 0)
			{
				delitem .itemIds[.@choice], countitem(.itemIds[.@choice]);
			}
			percentheal -10, -10;
			close;
		}
		if(.itemCureCount[.@correct] != .itemCount[.@count] || .itemCure[.@correct] != .itemIds[.@choice])
		{
			next;
			mes .@npcname$;
			mes "You're wrong. That's not the cure's recipe.";
			delitem .itemIds[.@choice], .itemCount[.@count];
			close;
		}
		else
		{
			delitem .itemIds[.@choice], .itemCount[.@count];
			set .@correct, .@correct + 1;
		}
	}
	next;
	mes .@npcname$;
	mes "Humpf...Allright...You've found it...";
	next;
	//condition fullfilled
	set .@skuname$, "[Skuld]";
	cutin "shaloshi01",2;
	mes .@skuname$;
	mes "I got him! I found his Future Thread. Take it to Verdandi now!";
	getitem $helSealItemId, 1;
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "On my way.";
	close;
	
OnSealActivated:
	set .@skuldName$, "[Skuld]";
	cutin "shaloshi01",2;
	next;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found he's weakness.";
	mes "To seal her, you'll have to find a cure to the sickness she spread around her.";
	mes "Talk to her again, and she'll have to let you try to compose it.";
	mes "Once done, come back to me. She'll be able to Seal him completely.";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'm on it.";
	set HELCUREQ, 1;
	close;
	
OnActivateTarget:
	set .@activatedRank, rand(1,5);
	cutin "shaloshi01",2;
	set .@skuldName$, "[Skuld]";
	explode(.@id$, strnpcinfo(3), "_");
	set .@realid$, .@id$[1] + "_"  + .@id$[2];
	set .@npcname$, .@id$[0] + "_"  + .@realid$ + "_" + .@activatedRank;
	hideoffnpc .@npcname$;
	mes .@skuldName$;
	mes "" + strcharinfo(0) + ", I've found her.";
	mes "She's on Comodo Field 0" + .@activatedRank + ".";
	next;
	cutin "", 255;
	mes "[" + strcharinfo(0) + "]";
	mes "I'll get her.";
	set .@query$, "UPDATE `verdandi_quests` SET `ActivatedNpc`=" + .@activatedRank + " WHERE `id`='" + .@realid$ + "'";
	query_sql(.@query$, .@count);
	close;
	
OnSealTarget:
	hideonnpc strnpcinfo(3);
	hideonnpc "Giant_2_2_2";
	hideonnpc "Giant_2_2_3";
	hideonnpc "Giant_2_2_4";
	query_sql("UPDATE `verdandi_quests` SET `generalState`=3 WHERE `id`='2_2'", .@count);
	announce " [Verdandi] The Giant Hel has been Sealed successfully thanks to " + strcharinfo(0) + " and his team.",bc_all,"0x00FF99";
	getpartymember getcharid(2),2;
	for( set .@pt, 0; .@pt < $@partymembercount; set .@pt, .@pt+1 ){
		if( attachrid( $@partymemberaid[.@pt] ) ){
			callfunc("AddPoints", getcharid(3), $sealingPointAmount);
			logmes "" + strcharinfo(0) + " obtains " + ($sealingPointAmount) + " points from sealing Hel.";
		}
	}
	close;
	
OnPCLoadMapEvent:
	query_sql("SELECT `generalState` FROM `verdandi_quests` WHERE `id`='2_2'", .@generalState);
	if (.@generalState[0] != 3)
	{	
		set .@found, 0;
		for(set .@i,0; .@i<getarraysize(.influencedMaps$) && .@found == 0; set .@i,.@i+1)
		{
			if(strcharinfo(3) == .influencedMaps$[.@i])
				set .@found, 1;
		}
		if (.@found == 1 && strcharinfo(3) != HELWARNED$)
		{
			//malus
			specialeffect2 127;
			sc_start SC_INCALLSTATUS, 60000, -10;
			addtimer 60000, strnpcinfo(3)+"::On1Min";
			dispbottom " [ Hel ] This place is mine. As long as it will be, you'll feel sick while getting here.";
			set HELWARNED$, strcharinfo(3);
		}
	}
	end;

On1Min:
	if(strcharinfo(3) == HELWARNED$)
	{
		specialeffect2 136;
		sc_start SC_INCALLSTATUS, 60000, -10;
	}
	end;
	
OnInitializeCureArray:
	copyarray  .@possibleHerbs[0], .itemIds[0], getarraysize(.itemIds);
	set .@count, 0;
	for(set .@i,0; .@i<.herbCountNeeded; set .@i,.@i+1)
	{
		set .@rank, rand(0,getarraysize(.@possibleHerbs) - 1);
		set .itemCure[.@count], .@possibleHerbs[.@rank];
		set .@rankCount, rand(0,getarraysize(.itemCount) - 1);
		set .itemCureCount[.@count], .itemCount[.@rankCount];
		deletearray .@possibleHerbs[.@rank], 1;
		set .@count, .@count + 1;
	}
	end;

OnInitMalusSpread:
	for(set .@i,0; .@i<getarraysize(.influencedMaps$); set .@i,.@i+1)
	{
		setmapflag .influencedMaps$[.@i], mf_loadevent;
	}
	return;

OnTimer10000:
	if (strnpcinfo(3) == "Giant_2_2_1")
	{
		query_sql("SELECT `ActivatedNpc`,`generalState` FROM `verdandi_quests` WHERE `id`='2_2'", .@npc$, .@generalState);
		if(.@npc$[0] != "0" && .@generalState[0] == 2)
		{
			hideoffnpc "Giant_2_2_" + .@npc$[0];
		}
		stopnpctimer;
	}
	end;
	
OnInit:
	hideonnpc strnpcinfo(3);
	initnpctimer;
	setarray .itemIds[0], 507, 508, 509, 510, 511;
	setarray .itemCount[0], 1, 3, 5, 10;
	set .herbCountNeeded, 5;
	set $helSealItemId, 27012;
	setarray .influencedMaps$[0], "cmd_fild01"; //, "cmd_fild02", "cmd_fild03", "cmd_fild04", "cmd_fild05", "cmd_fild06", "cmd_fild07", "cmd_fild08", "cmd_fild09";
	if(strnpcinfo(3) == "Giant_2_2_1")
	{
		initnpctimer;
		callsub OnInitMalusSpread;
		goto OnInitializeCureArray;
	}
	end;
}

cmd_fild06,76,388,4	duplicate(Giant_2_2_1)	Hel::Giant_2_2_2	4_JP_2015EVT
cmd_fild07,140,300,4	duplicate(Giant_2_2_1)	Hel::Giant_2_2_3	4_JP_2015EVT
cmd_fild08,194,175,4	duplicate(Giant_2_2_1)	Hel::Giant_2_2_4	4_JP_2015EVT