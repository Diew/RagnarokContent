ra_san05,149,144,5	script	BloodCrystal#11::alabc31	OBJ_A2,{
	set .@npcname$, "[Blood Crystal]";
	mes .@npcname$;
	mes "Current fill level: " + .fillPercent + "%"; 
	mes "Would do you want to do?";
	switch(select("Fill the Crystal","Retrieve a Spirit Life","Nothing"))
	{
		case 1:
			next;
			mes .@npcname$;
			mes "You're about to fill Life in Blood Crystal.";
			mes "How many Shards do you wish to give to the Crystal?";
			mes "'0' means 'as much as possible'.";
			input .@shardCount;
			if(.@shardCount == 0)
			{
				set .@shardCount, countitem($shardIds[0]);
			}
			if(.@shardCount > countitem($shardIds[0]))
			{
				next;
				mes .@npcname$;
				mes "You don't have those items.";
				close;
			}
			if(.popAvailable != 1)
			{
				next;
				mes .@npcname$;
				mes "The Guardian has allready been killed. Wait for " + $repopHourCount + " hours to summon it again.";
				close;
			}
			delitem $shardIds[0], .@shardCount;
			set .@fillRequired, 1000 + atoi(strnpcinfo(2)) * 50;
			set .fillItemCount, .fillItemCount + .@shardCount;
			set .fillPercent,  .fillItemCount * 100 / .@fillRequired;
			if(.fillPercent >= 100)
			{
				set .fillPercent, 100;
				next;
				mes .@npcname$;
				mes "The Crystal has been filled.";
				for(set .@i,.mapToCheck[0]; .@i<=.mapToCheck[1]; set .@i,.@i+1)
				{
					set .@mobCount, .@mobCount + mobcount("ra_san0" + .@i, "CardinalSystem::OnNormalDied");
				}
				if(.@mobCount == 0)
				{
					set .@percentHeal, 0 - atoi(strnpcinfo(2));
					percentheal .@percentHeal, .@percentHeal;
					set .popAvailable, 0;	
					setnpcdisplay strnpcinfo(3), OBJ_B2, 1;
					//special effect
					specialeffect 862;
					//summon boss
					monster strnpcinfo(4), 0, 0, "--ja--", .bossId, 1, "CardinalSystem::OnGuardianDied";
					initnpctimer;
					set .loopCount, 0;
				}
				else
				{
					mes "The Guardian will appears after total dungeon cleaning.";
				}
				setd("$" + strnpcinfo(3), 1);
			}
			else
			{
				next;
				mes .@npcname$;
				mes "The Crystal has been filled up to " + .fillPercent + "%";
			}
			break;
		case 2:
			next;
			mes .@npcname$;
			if(SPIRITLIFE == $maxSpiritLife)
			{
				mes "You can't get more than " + $maxSpiritLife + " Spirit Lifes.";
				close;
			}
			if(SPIRIT == 0)
			{
				mes "You can't retrieve a Spirit Life while you're not in Spirit mode.";
				close;
			}
			if(.fillPercent > $minFillPercentToRetrieveLife)
			{
				set .fillPercent, 0;
				set SPIRITLIFE, SPIRITLIFE + 1;
				set SPIRIT, 0;
				specialeffect2 372;
				undisguise();
				mes "The Crystal has been emptied. You've raised from the dead.";
			}
			else
			{
				mes "The Crystal isn't filled enough to allow Spirit Life retrieve.";
			}
			break;
		case 3:
			next;
			mes .@npcname$;
			mes "Action cancelled.";
			break;
	}
	close;

OnTimer360000:
	stopnpctimer;
	set .loopCount, .loopCount + 1;
	if(.loopCount >= $repopHourCount)
	{
		goto OnClearGauge;
	}
	initnpctimer;
	end;

OnClearGauge:
	setnpcdisplay strnpcinfo(3), OBJ_A2;
	set .fillPercent, 0;
	set .fillItemCount, 0;
	end;
	
OnPopMonsters:
	callsub OnClearMonsters;
//== F1 =============================
	monster "ra_san01", 0, 0, "--ja--", 1771, 82, "CardinalSystem::OnNormalDied";
	monster "ra_san01", 0, 0, "--ja--", 1772, 36, "CardinalSystem::OnNormalDied";
	monster "ra_san01", 0, 0, "--ja--", 1632, 10, "CardinalSystem::OnNormalDied";
	monster "ra_san01", 0, 0, "--ja--", 1633, 6, "CardinalSystem::OnNormalDied";
	monster "ra_san01", 0, 0, "--ja--", 1773, 6, "CardinalSystem::OnNormalDied";
	
//== F2 =============================
	monster "ra_san02", 0, 0, "--ja--", 1772, 85, "CardinalSystem::OnNormalDied";
	monster "ra_san02", 0, 0, "--ja--", 1771, 30, "CardinalSystem::OnNormalDied";
	monster "ra_san02", 0, 0, "--ja--", 1632, 10, "CardinalSystem::OnNormalDied";
	monster "ra_san02", 0, 0, "--ja--", 1633, 10, "CardinalSystem::OnNormalDied";
	monster "ra_san02", 0, 0, "--ja--", 1773, 3, "CardinalSystem::OnNormalDied";
	monster "ra_san02", 0, 0, "--ja--", 1774, 2, "CardinalSystem::OnNormalDied";

//== F3 =============================
	monster "ra_san03", 0, 0, "--ja--", 1773, 60, "CardinalSystem::OnNormalDied";
	monster "ra_san03", 0, 0, "--ja--", 1774, 40, "CardinalSystem::OnNormalDied";
	monster "ra_san03", 0, 0, "--ja--", 1632, 5, "CardinalSystem::OnNormalDied";
	monster "ra_san03", 0, 0, "--ja--", 1633, 5, "CardinalSystem::OnNormalDied";

//== F4 =============================
	monster "ra_san04", 0, 0, "--ja--", 1770, 60, "CardinalSystem::OnNormalDied";
	monster "ra_san04", 0, 0, "--ja--", 1769, 20, "CardinalSystem::OnNormalDied";
	monster "ra_san04", 0, 0, "--ja--", 1774, 10, "CardinalSystem::OnNormalDied";
	monster "ra_san04", 0, 0, "--ja--", 1773, 5, "CardinalSystem::OnNormalDied";

//== F5 =============================
	monster "ra_san05", 0, 0, "--ja--", 1769, 80, "CardinalSystem::OnNormalDied";
	monster "ra_san05", 0, 0, "--ja--", 1770, 30, "CardinalSystem::OnNormalDied";
	monster "ra_san05", 0, 0, "--ja--", 1774, 20, "CardinalSystem::OnNormalDied";
	monster "ra_san05", 0, 0, "--ja--", 1773, 20, "CardinalSystem::OnNormalDied";
	monster "ra_san05", 0, 0, "--ja--", 1772, 10, "CardinalSystem::OnNormalDied";
	end;
	
OnClearMonsters:	
OnClock0000:
	killmonsterall "ra_san01";
	killmonsterall "ra_san02";
	killmonsterall "ra_san03";
	killmonsterall "ra_san04";
	killmonsterall "ra_san05";
	set .popAvailable, 1;
	setd("$" + strnpcinfo(3), 0);
	return;
	
OnInit:
	set .fillPercent, 0;
	if(getd("$" + strnpcinfo(3)) >= 1)
	{
		set .fillPercent, 100;
	}
	setarray .mapToCheck[0],1,5;
	set .bossId, 1768;
	set .popAvailable, 1;
	end;
}

