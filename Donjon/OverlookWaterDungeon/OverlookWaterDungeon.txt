
gef_fild07,264,286,0	script	Overlook Water Dungeon::ovwadu	2_BULLETIN_BOARD,{
	if(VIP == 1 || countitem(.entranceItem) >= .entranceItemCnt)
	{
		mes "[Overlook Water Dungeon]";
		mes "Would you like to enter in the Overlook Water Dungeon?";
		if(VIP == 0)
		{
			mes "It'll cost you " + .entranceItemCnt + " " + getitemname(.entranceItem) + ".";
		}
		if(select("Enter","Leave") == 1)
		{
			if(BaseLevel < .level[0] || BaseLevel > .level[1])
			{
				next;
				mes "[Overlook Water Dungeon]";
				mes "Sorry, this dungeon is restricted to level " + .level[0] + " to " + .level[1];
				close;
			}
			if(VIP == 0 && countitem(.entranceItem) >= .entranceItemCnt && gettimetick(2) > .owdDelay)
			{
				delitem .entranceItem, .entranceItemCnt;
				//3 days delay
				set .owdDelay, gettimetick(2) + 259200;
			}
			warp .owdMap$, .owdX, .owdY;
		}
	}
	else
	{
		mes "[Overlook Water Dungeon]";
		mes "This is the entrance to Overlook Water Dungeon.";
		mes "^FF0000Restricted Level: 30 - 85^000000";
	}
	close;
	
OnInit:
	set .entranceItem, 6320;
	set .entranceItemCnt, 2;
	set .owdMap$, "iz_dun00t";
	set .owdX, 168;
	set .owdY, 168;
	setarray .level[0],30,85;
	end;
}

iz_dun00t,174,169,0	script	Overlook Water Dungeon::ovwadub	2_BULLETIN_BOARD,{
	if(OWDCurQuest == -1)
	{
		set OWDCurQuest, 0;
	}
	if(checkquest(.questIds[OWDCurQuest]) == -1)
	{
		setquest .questIds[OWDCurQuest];	
	}
	if(OWDCurQuest >= getarraysize(.questsIds))
	{
		mes "[Quest Board]";
		set .@ok, 1;
		for(set .@i,0; .@i<getarraysize(.questIds); set .@i,.@i+1)
		{
			if(checkquest(.questIds[.@i]) != 2)
			{
				set .@ok, 0;
			}
		}
		if(OWDQuest == 1)
		{
			set .@ok, 0;
		}
		if(.@ok == 1)
		{
			mes "You've completed all quests.";
			mes "Choose a reward.";
			for(set .@i,0; .@i<getarraysize(.finalRewardItem); set .@i,.@i+1)
			{
				set .@menu$, .@menu$ + (.@i == 0 ? "" : ":") + " " + getitemname(.finalRewardItem[.@i]);
			}
			set .@choice, select(.@menu$) - 1;
			getitem .finalRewardItem[.@choice], 1;
			set OWDQuest, 1;
		}
		else
		{
			mes "You've allready completed this quest.";
		}
	}
	else if(checkquest(.questIds[OWDCurQuest]) < 2)
	{
		if(checkquest(.questIds[OWDCurQuest], HUNTING) == 2)
		{
			completequest .questIds[OWDCurQuest];
			
			mes "[Quest Board]";
			mes "Please, choose a reward.";
			set .@menu$, "";
			for(set .@i,OWDCurQuest * 2; .@i<(OWDCurQuest * 2 + 2); set .@i,.@i+1)
			{
				if(.rewardItem[.@i] != .@lastItem)
				{
					set .@menu$, .@menu$ + (.@i == (OWDCurQuest * 2) ? "" : ":") + " " + getitemname(.rewardItem[.@i]);
					set .@lastItem, .rewardItem[.@i];
					set .@tempRewId[getarraysize(.@tempRewId)], .@lastItem;
				}
			}
			set .@choice, select(.@menu$) - 1;
			next;
			mes "[Quest Board]";
			mes "Quest complete. Reward: " + getitemname(.@tempRewId[.@choice]);
			getitem .@tempRewId[.@choice], 1;
			getexp .rewardExp[OWDCurQuest * 2], .rewardExp[OWDCurQuest * 2 + 1];
			set OWDCurQuest, OWDCurQuest + 1;
		}
		else
		{
			callsub OnDisplayReq;
		}
	}
	close;

OnDisplayReq:
	mes "Kill those monsters to get a reward:";
	for(set .@i,OWDCurQuest * 4; .@i<OWDCurQuest * 4 + 4; set .@i,.@i+2)
	{
		mes " " + .mobReq[.@i + 1] + " " + strmobinfo(1, .mobReq[.@i]);
	}
	return;	
	
OnInit:
	setarray .mobReq[0],2512,20,2506,20,2515,40,2504,40,2510,60,2508,60,2505,80,2511,80,2513,100,2509,100;
	setarray .rewardExp[0],60600,68040,88695,99900,155700,174960,218475,245925,335340,377190;
	setarray .rewardItem[0],2102,2104,2504,2506,985,985,2608,2608,2608,985;
	setarray .finalRewardItem[0],4027,4049,4024,4035,4045,4055,4091,4093,4199,4105,4077,4084,4089,4111,4125;
	setarray .questIds[0],50000,50001,50002,50003,50004;
	end;
}